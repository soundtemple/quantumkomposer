@OnLoad
  SetShortName {ATOMSQ} 
	LabelPads {ATOM SQ REMAPPER}
  ShowLayout 2
	shift_a=0
	shift_b=0
	rhs_keymode=0
	curr_chan=4
	btn_ccs=[82,83,84,85,109,99]
	knob_vals = [90,90,90,90, 0,0,64,90]
	lhs_notes=[36,40,43,47, 48,52,55,59, 60,64,67,71, 72,76,79,83] //notes used
	lhs_notes_a=[36,40,43,47, 48,52,55,59, 60,64,67,71, 72,76,79,83] //chordds thru kords
	lhs_notes_b=[49,51,54,56, 58,61,63,66, 60,61,62,63, 64,65,66,67] //rmkr and chromo 
	notes_ch_select[74]=15
	notes_ch_select[72]=14
	notes_ch_select[71]=13
	notes_ch_select[69]=12
	notes_ch_select[67]=11
	notes_ch_select[65]=10
	notes_ch_select[64]=9
	notes_ch_select[62]=8
	notes_ch_select[60]=7
	notes_ch_select[59]=6
	notes_ch_select[57]=5
	notes_ch_select[55]=4
	notes_ch_select[53]=3
	notes_ch_select[52]=2
	notes_ch_select[50]=1
	notes_ch_select[48]=0
	notes_ch_select[23]=7
	notes_ch_select[22]=6
	notes_ch_select[21]=5
	notes_ch_select[20]=4
	notes_ch_select[19]=3
	notes_ch_select[18]=2
	notes_ch_select[17]=1
	notes_ch_select[16]=0
	notes_ch_select[15]=15
	notes_ch_select[14]=14
	notes_ch_select[13]=13
	notes_ch_select[12]=12
	notes_ch_select[11]=11
	notes_ch_select[10]=10
	notes_ch_select[9]=9
	notes_ch_select[8]=8
	Call @LabelAllPads
@End

@OnMidiCC
  if MIDIChannel=0 and MIDIByte2=1 //modwheel
		SendMIDIThruOnCh curr_chan //active channel selected for notes
	elseif MIDIChannel=0 and MIDIByte2=85 and MIDIByte3>0
	  SendMIDICC 15, 111, 127 //Transport STOP
	elseif MIDIChannel=0 and MIDIByte2=86 and MIDIByte3>0
		SendMIDICC 15, 112, 127 //Transport PLAY
	elseif MIDIChannel=0 and MIDIByte2=87
		shift_a=MIDIByte3 //use keys to select midi channel 
		Call @LabelAllPads 
	elseif MIDIChannel=0 and MIDIByte2=89
		shift_b=MIDIByte3 //use keys to select ch and mgen guis
		Call @LabelAllPads
	elseif MIDIChannel=0 and MIDIByte2=102 //arrow key left - keymode CH_SELECT
		rhs_keymode=0
		Call @LabelAllPads
	elseif MIDIChannel=0 and MIDIByte2=103 //arrow key top - to KNTRL
		if MIDIByte3>0
			SendMIDICC 15, 18, 127
		endif
	elseif MIDIChannel=0 and MIDIByte2=104 //arrow key middle - KNTRL SHIFT
		if MIDIByte3>0
			SendMIDINoteOn 15, 16, 127
		endif
	elseif MIDIChannel=0 and MIDIByte2=105 //arrow key right - keymode KNTRL_KNTRL
		rhs_keymode=1
		Call @LabelAllPads
	elseif MIDIChannel=2 and (MIDIByte2>=24 and MIDIByte2<=29) //6 song btns used as shifted modes
		if MIDIByte3 > 0 //goto mutes, solos, arms, aum, autom, atomsq
			SendMIDICC 15, btn_ccs[MIDIByte2-24], MIDIByte3
		endif
	else
	  SendMIDIThru //btns A-H and encoders thru. Set using editor
		if MIDIByte2>=76 and MIDIByte2<=81
			knob_vals[MIDIByte2-76]=MIDIByte3
			Call @LabelAllPads
		endif
		if MIDIByte2=19 or MIDIByte2=20
			knob_vals[MIDIByte2-13]=MIDIByte3
			Call @LabelAllPads
		endif
	endif
@End

@OnMidiNote
  if shift_a and shift_b
		Log {SHIFT A & B ACTIVE}
	elseif shift_a //KEYS OUT MIDI CH
		curr_chan = notes_ch_select[MIDIByte2]
		Call @LabelAllPads
		if curr_chan<=4
			CopyArray lhs_notes_b, lhs_notes, 16
		else
		  CopyArray lhs_notes_a, lhs_notes, 16
		endif
	elseif shift_b
		Log {SHIFT B ACTIVE} //switch MGEN GUI
	elseif MIDIByte2>=16 and MIDIByte2<=31 // LHS Special Notes Mode
		if MIDICommand=0x90
			Log {sending note on}
			SendMIDINoteOn curr_chan, lhs_notes[MIDIByte2-16], MIDIByte3
		elseif MIDICommand=0x80
			Log {sending note off}
			SendMIDINoteOff curr_chan, lhs_notes[MIDIByte2-16], MIDIByte3
		endif
	elseif MIDIByte2>=31 // LHS Notes Mode regular
		SendMIDIThruOnCh curr_chan
	elseif rhs_keymode=0 and MIDIByte2<=15 and MIDIChannel=15 //rhs keys ch sel mode
		if MIDIByte3>0
			if MIDIByte2 = curr_chan //sel chan pad twice - toggle instr gui
				SendMIDICC 16, 124, 127
			endif
			SendMIDICC MIDIChannel, MIDIByte2+24, 127
			curr_chan=MIDIByte2
		endif
	elseif rhs_keymode=1 and MIDIByte2<=15 and MIDIChannel=15 //rhs keys kntrl kntrl mode
		if MIDIByte3>0
			SendMIDIThru
		endif
	endif
@End

@OnMidiInput
  if MIDICommand=0xD0 or MIDICommand=0xE0 //Aftertouch or pitchbend
		SendMIDIThruOnCh curr_chan
	endif
@End


@LabelAllPads
	LabelPad 0, { ____}, curr_chan+1, {____},{ KEYS OUT}
	if shift_a and shift_b
	  LabelPad 1, {___SHIFT___},{ A&B}
	elseif shift_a
		LabelPad 1, {___SHIFT___},{ MID CH SEL}
	elseif shift_b
		LabelPad 1, {___SHIFT___},{ MGEN GUIS}
	else
	  LabelPad 1, {___SHIFT___},{ OFF }
 	endif
	if rhs_keymode=0
		LabelPad 2, {___RKEYS__},{ CH SEL}
	elseif rhs_keymode=1
		LabelPad 2, {___RKEYS__},{ KNTRL}
	endif
	LabelPad 3,  {___BTNSA__ }, {M - S - A}
	LabelPad 4,  {___BTNSB__ }, {SQ-AUM-AUT}
	LabelPad 8,  {___MIXA___ }, knob_vals[0]
	LabelPad 9,  {___MIXB___ }, knob_vals[1]
	LabelPad 10, {___MIXC___ }, knob_vals[2]
	LabelPad 11, {___MIXD___ }, knob_vals[3]
	LabelPad 12, {___RTNA___ }, knob_vals[4]
	LabelPad 13, {___RTNB___ }, knob_vals[5]
	LabelPad 14, {___MGAIN__ }, knob_vals[6]
	LabelPad 15, {___MVOL___ }, knob_vals[7]
@End