//# QUANTUM KONTROLLER SUITE
//# KORDS APP
@OnLoad 
  ShowLayout 2
  LabelPads {[ QK:KORDS ]}
  SetShortName {KORDS}
	if unassigned scene_bank //# duration in first scene
		ResetNoteStates FALSE // #reset the locker with the value FALSE!
	  mode = 0 //# See @HandleModeChange for modes
	  songmode = 0 //# 0:Playsong 1:SceneLocked 2:ChordLocked
	  SetMetroPPQN 4
	  ppqn = 4 
		ShowLayout 2
		scene_change_requested = -1 //# on pad down has next scene number
	  chord_change_requested = -1 //# on pad down has next chord number
		chord_beat_count = -1
		scene_beat_count = -1
		scene_changed = FALSE
	  scene_change_chord_reset = TRUE //# Goto chord 0 when user changes scene
	  in_mode_select = TRUE //# Toggle for changing modes mode
	  number_of_modes = 16 //# Change in operational mode 
	  base_starting_note = 48 //# centre point for notes out
		allow_mode_chg_playbk = TRUE //# Mode change during playback
	  return_to_song_mode_requested = FALSE //# used for switching on new bar 
	  rec_chord_note_count = 0 //# recording notes in chord slot
	  use_turn_around = TRUE //# last chord as turn around on last bar of scene
		//# used for LED feedback
		last_pad=0 
		mgen_device_id=8 //used for midi control of pads. each mgen instance is assigned a number
		mgen_sel_dev=0 //used for midi control of pads
		shift_is_down=FALSE
		pad_is_down=FALSE
	  led_index=20 // notes 20-46 used for updating LED feedback on 16pads 
		send_cc_back_to_knobs = TRUE
		knobs_controller=[15,40,41,42,43] //midi controller ch & 4 base ccs. cc's=cc's+(4xmgen_device_id
		txpose_w_circle_fifths=FALSE
		copy_mode=-1
		generate_random_chord=FALSE
		SetTimerInterval 1000
		Call @TransposeCalcArrays
	  Call @InitKnobVariables //# All knob settings on start
		Call @SysexMessageSetup
	  Call @SetupChordsScenes
	  knob_set=99
		Call @SetupKnobset99 //settings knobs
	  Call @SetupSettingsLayout
	endif
@End

@SysexMessageSetup
	//QK Sysex messages. from KNTRL to BEATS and from BEATS to midi controllers
	qk_sysex_header=[0x3F,0x40,0x3F,0x40,0x3F,0x40] //QK messages
	len_header=6 
	//QK message types 
	sx_scene_change=100 //scene change
	sx_scene_durations=101 //scene durations
	sx_rtn_to_song=22 //return to song 
	sx_glbl_swing=23 //global swing
	sx_copy_scene=89 //Copy paste contents of whole scene
	sx_mgen_knobs=40 //MGEN 4 knobs --to knobs controller
@End

@InitKnobVariables
	//# Any knobset knob should have a setting here
	scn_duration = 0 
	scn_txpose = 0 
	scn_preset = 1
	scn_out4_on = 1
	num_sc_presets = 13
	chrd_duration = 0
	chrd_inversion = 0
	slct_chord_root = 0 //# Select chord mode only
  slct_chord_type = 0 //# Select chord mode only
  slct_chord_inversion = 0 //# Select chord mode only
	//# Construct chords
	constructed_root = 0
	constructed_3rd = 2
	constructed_5th = 1
	constructed_7th = 3	
	record_notes_channel=4
  FillArray sort_list, 0 //# empty array for sorting chord notes as arrive

  //# Selecting chords from types mode
  num_chord_types = 11 //# Chord types for select mode
  chord_types[0] = [4,7,-1] //# Major
  chord_types[5] = [3,7,-1] //# Minor
  chord_types[10] = [3,6,-1] //# Diminished
  chord_types[15] = [4,8,-1] //# Augmented
  chord_types[20] = [2,7,-1] //# Sus2
  chord_types[25] = [5,7,-1] //# Sus4
  chord_types[30] = [3,7,8] //# Minor 6th
  chord_types[35] = [4,7,9] //# Major 6th
  chord_types[40] = [3,7,10] //# Minor 7th
  chord_types[45] = [4,7,10] //# Dom 7th
  chord_types[50] = [4,7,11] //# Major 7th

	//# Intervals for constructing chords
	c3rd_intervals = [2, 3, 4, 5] //# sus2, min, maj, sus4
	c5th_intervals = [6, 7, 8] //# dim (b5), P5, aug (#5)
	c7th_intervals = [-1, 8, 9, 10, 11] //# min6 (b6), maj6, min7 (b7), maj7	
	
	//keeping track of held notes over chord changes
	//When a chrd chgs. Turn off all held notes and replace with notes of new chord
	update_held_notes_on_chord_change=TRUE //FALSE will allow held notes to continue as originally triggered
	num_mel_voices=6 //melody chans max polyphony. for tracking melody notes over chord changes we limit for CPU
	FillArray mel_note_voices, -1, num_mel_voices*16
	FillArray held_chord_notes, -1, 16 //Tracking the chord note chans. 1 note per chan max IN.
	FillArray held_chord_oct_adj, 0, 16 	//Harmony note octave in effect octave chord notes are sent out on
@End

@SetupChordsScenes
	current_scene = 0 // #0-7 Pads 0-7
  current_chord = 0 // #0-7 Pads 8-15
	chord_slot=0
	//# scene config: [duration, txpose, preset, mg1-4_modes, out_ch1-4 modes]
  //# MG Modes determine how harmonization occurs per scene
  sc_txp_slot = 1
  sc_preset_slot = 2
	sc_size = 15 //# ^ above slots + duration = 11 + 4 spare for even sizing 
  rnd_scn_chance=0 //% chance scene change will be to a random one
	rnd_chrd_chance=0 //% chance chord change will be to a random one
	num_scns_with_dur=0 //RND only selects scenes with duration
	num_chrds_with_dur=[0,0,0,0, 0,0,0,0] //RND only selects chords with duration per scene
	//# chord = [note1, note2, note3, note4, duration, inv, bass, suggestion]
  dur_slot = 4 //# fixed location for duration
  inv_slot = 5
  bass_slot = 6 //not used
  sugg_slot = 7 //# for chord suggestion selected choice
 
  //# Create empty chord and scene arrays
  for s = 0 to 7 
	  //# Create 8 scene arrays in scene_bank
	  scene_bank[s*sc_size] = [0,6,1,1,1,1,1,8,8,8,8,-1]
		for c = 0 to 7
			//# Create 8 chord arrays for each scene
			chord_bank[(s*100) + (c*10)] = [-1, -1, -1, -1, 0, 0, -1, 0]
		endfor
	endfor
	
  //# indexing them by scene size (sc_size). Sample scenes
  //# eg scene2 ... scene_bank[sc_size * sc#(0-7)] = [2,6,0,0,0,0,0,1,1,1,1]
  scene_bank[0] = [1,6,1,1,1,1,1,8,8,8,8,-1]
	//# Example chords: increment in 10's in each scene, new scenes on the 100
  //# eg: chord_bank[10] = [67, 71, 74, 77, 8, 0, -1] //# Scene1 Chord 2
  //# eg: chord_bank[100] = [65, 69, 72, 76, 8, 0, -1] //# Scene2 Chord 1
  //# eg: chord_bank[110] = [69, 72, 76, 79, 4, 0, -1] //# Scene2 Chord 1
  
	FillArray all_chord_scales, -1, 1024 //store for the scale used when each chord (up to 64) is active 
	FillArray current_chord_scale, -1, 12 //the active chord_scale copied from all_chord_scales
	FillArray chord_notes_w_inv, -1, 1024 //store for the playable chord note pitches for each chord post inversion + txpose
	curr_chord_notes=[-1,-1,-1,-1,-1,-1] //lo, root, 3rd, 5th, 7th current chord post txpose, inversion, raw octave
	mode_midi_channel=0 //editing the midi channel mode
	//map incoming note to get inversion....C=root invert up or down from C
	inversions_for_gated_chord=  [0,1,2,3,4,0,0,0,-4,-3,-2,-1]
	//triad inversions
	//we use these for mode2 gated chord for quick access to adjust chord_scale notes for inversion 
	triad_inversion_adjustments[0] = [-12,-12,-12,0] //inversion -4 
	triad_inversion_adjustments[4] = [-12,-12,-12,0] //inversion -3 
	triad_inversion_adjustments[8] = [0,-12,-12,0] //inversion -2 
	triad_inversion_adjustments[12]= [0,0,-12,0] //inversions -1 
	triad_inversion_adjustments[16]= [0,0,0,0] //root inversion 
	triad_inversion_adjustments[20]= [12,0,0,0] //1st inversion 
	triad_inversion_adjustments[24]= [12,12,0,0] //2nd inversion 
	triad_inversion_adjustments[28]= [12,12,12,0] //3rd inversion 
	triad_inversion_adjustments[32]= [12,12,12,0] //4th inversion 
	//tetrad inversions 
	tetrad_inversion_adjustments[0] = [-12,-12,-12,-12] //inversion -4
	tetrad_inversion_adjustments[4] = [0,-12,-12,-12] //inversion -3
	tetrad_inversion_adjustments[8] = [0,0,-12,-12] //inversion -2
	tetrad_inversion_adjustments[12]= [0,0,0,-12] //inversions -1
	tetrad_inversion_adjustments[16]= [0,0,0,0] //root inversion
	tetrad_inversion_adjustments[20]= [12,0,0,0] //1st inversion
	tetrad_inversion_adjustments[24]= [12,12,0,0] //2nd inversion
	tetrad_inversion_adjustments[28]= [12,12,12,0] //3rd inversion
	tetrad_inversion_adjustments[32]= [12,12,12,12] //4th inversion
	//Modes for processing incoming notes 0:Off 1:Harmonize 2:GatedChord 3:ChordNotes 4:OFF
	midi_chan_modes=[0,0,0,0, 1,2,1,2, 2,3,3,3, 0,0,0,0] 
	set_all_mc_to=1 //Setting all midi channels to x def is thru
	active_ch_range=4 //limit active channels for better cpu 0-3; 0-7; 4-11; 0-11; 0-15
	active_ch_min=0 //for setting active_ch_range 
	active_ch_max=15 //for setting active_ch_range
	FillArray mel_note_velos, 100, 127 //default value for stored velocity when re-trig mel notes on chord change 
	apn_adj=0 //apn_adj allow passing notes  0:no 1:yes
  //# Scale definitions for midi in key, scale handling
  //# Allowed scales for midi in
	num_scale_types = 2
  allowed_scales = [1,2] 
  //# incoming midi key, scale. default C Major
	selected_midi_in_root = 0 // # root note -11 (C-B) Set in Knobset5
  selected_midi_in_scale = 0 // #see allowed_scales  
  midi_in_scales[10] = [0,2,4,5,7,9,11]  //# major
	midi_in_scales[20] = [0,2,3,5,7,8,10] // #minor_nat
	midi_in_scales[30] = [0,2,3,5,7,9,10]  // #minor_mel
	midi_in_scales[40] = [0,2,3,5,7,9,11]  // #minor_har
	Call @GetMidiInScale
@End

@GetMidiInScale
	PresetScale allowed_scales[selected_midi_in_scale]
	SetRootNote selected_midi_in_root
	// #create an array with the notes of selected key scale
	scale_loc = allowed_scales[selected_midi_in_scale] * 10
	for scale_degree = 0 to 6
		// #ms is incoming_midi_scale
		midi_in_scale_notes[scale_degree] = (selected_midi_in_root + midi_in_scales[scale_loc + scale_degree]) % 12 
	endfor
	//Log {Passing notes: }, (NoteName midi_in_scale_notes[1]), { }, (NoteName midi_in_scale_notes[3]), { }, (NoteName midi_in_scale_notes[5])
	//Log {Play chord notes with: }, { }, (NoteName midi_in_scale_notes[0]), { }, (NoteName midi_in_scale_notes[2]), { }, (NoteName midi_in_scale_notes[4]), { }, (NoteName midi_in_scale_notes[6])
	//Log {Incoming melody scale: }, RootNoteName, { }, ScaleName
@End

@OnHostStart
	//log {--- HOST START ---}
	//# Start beat count on same beat as host
	//# In AUM getting HostBeat to start on 0 seems flaky
	//# Fixed by BramBos in June2020
	//Log HostBar, { : }, HostBeat, { = }, (HostBeatsPerMeasure * HostBar) + HostBeat
	chord_to_set=current_chord
	Call @SetChordScale 
	if (HostBeat = 0) and (HostBar = 0)
		//# starting playback from beginning		
		chord_beat_count = -1
		scene_beat_count = -1
		scene_changed = FALSE
		if (songmode = 0)
			current_scene = 0
			current_chord = 0
		elseif (songmode = 1)
			current_chord = 0
		endif
		if mode > 2 
			//# No chord Rec, Select or Delete in playback
			mode = 0
		endif
	else
		//# continuing playback
		//# Could do nothing and allow continue with vars unchanged
		//# but in case use can jump timeline we are doing this to keep HostBeat aligned and chord/Scene at same relative positions.
		scene_completed_bars = scene_beat_count % HostBeatsPerMeasure
		chord_completed_bars = chord_beat_count % HostBeatsPerMeasure
		scene_beat_count = scene_completed_bars + HostBeat
		chord_beat_count = chord_completed_bars + HostBeat
	endif
	//Log {---- Host Started ----}
	Call @SetupLayout
@End

@OnHostStop
  Call @ResetBeatCounts
  //Log {---- Host Stopped ---- }, scene_beat_count, { - }, chord_beat_count 
  Call @SetupLayout
	//reset held notes
	Call @TurnOffChordNotes
	FillArray mel_note_voices, -1, num_mel_voices*16
	FillArray held_chord_notes, -1, 16 //Tracking the chord note chans. 1 note per chan max IN.
	FillArray held_chord_oct_adj, 0, 16 	//Harmony note octave in effect octave chord notes are sent out on
@End 

@ResetBeatCounts
	//# Re-start when mode or scene changes
	scene_beat_count = 0
	chord_beat_count = 0
	//# Log {Beat counts reset}
@End

@OnMetroPulse
@End

@OnNewBar
	//Log {--- new bar ---}
@End 

@OnNewBeat
	//Log {-- new beat --}
	//# increment beat counts xonb
	scene_beat_count = scene_beat_count + 1
	chord_beat_count = chord_beat_count + 1
	
	//# Handle Scene and Chord changes
	current_scene_duration = scene_bank[current_scene * sc_size]  * HostBeatsPerMeasure //# IN BEATS!
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	current_chord_duration = chord_bank[chord_slot + dur_slot]
	
	current_host_beat = (HostBar * HostBeatsPerMeasure) + HostBeat
	
	//# Log {Scene beats left: }, current_scene_duration - scene_beat_count
	//# Log {Chord beats left: }, current_chord_duration - chord_beat_count
  time_for_turnaround = FALSE
  if use_turn_around
  	Call @TimeForTurnaround
  endif
  
	//# Chord progressop -- Order of operations
	if (scene_change_requested > -1) and (HostBeat = 0)
		//# user requested scene change and its a new bar (& return to song)
		//# Log {Handling scene change request. It a new bar}
		Call @HandleSceneChange
	elseif (scene_beat_count >= current_scene_duration)
		//# its time to increment scene
		Call @HandleSceneChange
	elseif (chord_change_requested > -1)
  	//# User requested chord change & its a new beat
 		Call @HandleChordChange
 	elseif time_for_turnaround
 		Call @HandleChordChange
	elseif (chord_beat_count >= current_chord_duration) and (current_chord_duration>0) 
		Call @HandleChordChange
	endif
	if mode=0
		LabelPads {[ KORDS ]    Scene}, current_scene+1, {   Bar}, (Div scene_beat_count, HostBeatsPerMeasure)+1, {   Beat }, scene_beat_count+1, {/}, (current_scene_duration) 
	endif
@End

@TimeForTurnaround
	//# Under these conditions jump to last chord for 1 bar before scene change
	//# 2nd last chord in scene has no duration. Last chord has 4 beat duration
	//# Mode must be in song or scene lock
	//# must be 1 bar before scene ends
	//# Log {**** CHECKING TURN AROUND ****}
	chord6 = chord_bank[(current_scene * 100) + 60 + dur_slot]
	chord7 = chord_bank[(current_scene * 100) + 70 + dur_slot]
	//# Log {Chord6 dur: }, chord6, { Chord7 dur: }, chord7
	//# Log {SBC: }, scene_beat_count, { SC_DUR: }, current_scene_duration, { HBPM: }, HostBeatsPerMeasure
	//# Log scene_beat_count, { === }, (current_scene_duration - HostBeatsPerMeasure)
	if (songmode <= 1) and (chord6 = 0) and (chord7 = 4) and (current_chord <= 5) and (scene_beat_count = current_scene_duration - HostBeatsPerMeasure)
		time_for_turnaround = TRUE
		//Log {Its TURN AROUND TIME!!}
	endif
@End

@HandleSceneChange
	//# Work out which scene is next 
	scene_changed = TRUE
	if return_to_song_mode_requested
		return_to_song_mode_requested = FALSE
	endif
  if scene_change_requested > -1
  	current_scene = scene_change_requested
		scene_change_requested = -1
	elseif (songmode > 0)
  	current_scene = current_scene
  elseif (current_scene = 7) //# End of song 
    current_scene = 0 //# TODO: Handle end of song
  elseif scene_bank[(current_scene * sc_size) + sc_size] <= 0 //# also song end
  	current_scene = 0 //# no duration in next scene return to beginning
  else
		current_scene = current_scene + 1 //# Going to next
  endif
  if ((Random 1,99) < rnd_scn_chance) and songmode=0 //%chance of random scene change if not scene locked
    rnd_scn = (Random 0, num_scns_with_dur) //get random scene with duration
		if scene_bank[rnd_scn * sc_size]>0 and chord_bank[(rnd_scn * 100) + dur_slot]>0
			current_scene=rnd_scn //if rnd_scn has scene and first chord duration
		endif
	endif
  Call @ResetBeatCounts
  //Log {----- SCENE CHANGE -----}, {S}, current_scene+1
  Call @HandleChordChange //# Scene change effect chord change
@End

@HandleChordChange
	//# Work out what the next chord is 
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	if (chord_change_requested > -1)
		current_chord = chord_change_requested
		chord_change_requested = -1 //# reset request flag
		if songmode < 2
			songmode = 2 //# Lock to chord if user requests chord change
 		endif
 	elseif (scene_change_requested > -1) //# also handle chord change
		current_chord = 0
  	scene_change_requested = -1
  	scene_changed = FALSE
  elseif scene_changed
		//# reset chord to 0 on scene change if songmode < 2
		if (songmode <= 1)
			current_chord = 0
		endif
		scene_changed = FALSE
	elseif (songmode = 2) //# Loop current chord & Scene
    current_chord = current_chord
  elseif (current_chord = 7) //# Wrap playback
    current_chord = 0
  elseif (time_for_turnaround)
  	//# play last chord for one bar as a turn around before scene change
  	current_chord = 7
  elseif (chord_bank[(chord_slot+10) + dur_slot] <= 0)
  	current_chord = 0 //# no duration no next chord (chord_slot + 10)
  else
    current_chord = current_chord + 1
  endif
	if ((Random 1,99) < rnd_chrd_chance) and songmode<2 //%chance of random chord change if not chord locked
    rnd_chrd = (Random 0, num_chrds_with_dur) //get random chord with duration
		if chord_bank[(current_scene * 100) + (rnd_chrd * 10) + dur_slot]>0
			current_chord=rnd_chrd //if rnd_chrd has duration
		endif
	endif
	chord_to_set=current_chord
	Call @SetChordScale
  chord_beat_count = 0 //# reset chord beat count on new chord
	//turn off notes over chord change and re-trigger melody and harmony channel notes
	Call @HandleNotesHeldOverChordChange 
	//# Label pads
  if not in_mode_select
	  LabelPads {[ KORDS ]  Scene: }, current_scene+1, { Chord: }, current_chord+1
  endif
  //Log {----- CHORD CHANGE -----}, {C}, current_chord+1
  Call @SetupLayout
@End

@HandleNotesHeldOverChordChange
	if HostRunning //turn off notes over chord change and re-trigger melody and harmony channel notes
		for c = active_ch_min to active_ch_max //active channels 1-15. 16 is reserved for midi input and kntrl
			chord_note_ch =  c
			melody_note_ch = c
			if midi_chan_modes[c]=1 and update_held_notes_on_chord_change //Harmonize 
	 			for v = 0 to (num_mel_voices-1)
					voice = (melody_note_ch*num_mel_voices)+v
					if mel_note_voices[voice] > -1 //ch1
						//turn off the existing melody ch note
						harmony_note = (GetNoteState melody_note_ch, mel_note_voices[voice])
						SendMIDINoteOff melody_note_ch, harmony_note, 64
						//Log {HCC Voice:}, v, { ch }, c+1, {mn voice in_note}, mel_note_voices[voice], { hnote: }, harmony_note 
						//retrigger the melody channel voice	
						midi_note = mel_note_voices[voice]
						note_in_velo = mel_note_velos[voice] 
						Call @ConvertToHarmonyNotes
					endif
					//Log {CHANGE Done with voice }, v+1
				endfor
			elseif midi_chan_modes[c]=2 and update_held_notes_on_chord_change
				Call @TurnOffChordNotes //turn off all chord notes on chord change & update notestate
				if held_chord_notes[c]>=0
					trigger_note_oct_adj = held_chord_oct_adj[c]  //notes 48-59 no change
					Call @SendOutChordNotes //Send out new notes for held chord notes & update notestate
				endif		
			elseif midi_chan_modes[c]=3 and update_held_notes_on_chord_change
				Call @TurnOffChordNotes //turn off all chord notes on chord change & update notestate
				if held_chord_notes[c]>=0
					trigger_note_oct_adj = 0  //notes 48-59 no change
					midi_note=held_chord_notes[c] // the incoming note determines what we send out
					Call @SendOutChordNotes //Send out new notes for held chord notes & update notestate
				endif		
			endif
		endfor
	endif
@End

@LogCurrentInfo
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	current_chord_duration = chord_bank[chord_slot + dur_slot]
	current_scene_duration = scene_bank[current_scene * sc_size]
	Log {Playing: Scene: }, current_scene+1, { }, scene_beat_count+1, {/}, (current_scene_duration * HostBeatsPerMeasure), { beats}, { Chord:}, current_chord+1, { }, chord_beat_count+1, {/}, current_chord_duration, { beats}, { Mode: }, mode, { HostBeat: }, HostBeat+1
@End

@OnPadDown
  last_pad = LastPad // so midi in can simulate LastPad also see OnMidiIn
	Call @OnPadDown2 //midi input cant trigger @OnPadDown, It calls @OnPadDown2
@End

@OnPadDown2
	//# Splitting this event into separate handlers
	shift_is_down=FALSE //protect against unwanted shift releases not happening
	pad_is_down=TRUE
	if in_mode_select and (last_pad <= (number_of_modes - 1))
		if (HostRunning and (last_pad > 0) and NOT allow_mode_chg_playbk)
			LabelPads {NOT ALLOWED WHEN HOST RUNNING}
			Exit //# Only return to song allowed in playback
		elseif last_pad=13
			Exit //# Not in use
		elseif (HostRunning and last_pad=8)
			Exit //# Not allowed in playback
		elseif (HostRunning and last_pad=0)
    	//# Return to song mode requested
    	return_to_song_mode_requested = TRUE
    	scene_change_requested = current_scene
    	in_mode_select = FALSE
    	songmode = 0
    	mode = 0
    	ColorPad 0, 3
	    ColorPad 8, 3
			if mgen_device_id=mgen_sel_dev
				SendMIDINoteOn 15, 0+led_index, 3
				SendMIDINoteOn 15, 8+led_index, 3
			endif
		elseif (last_pad = 1)
			in_mode_select = FALSE
			mode = 0
			knob_set = 1
			Call @SetupLayout
		elseif (last_pad>=2 and last_pad<=4) //# +CHORDS MODES
			mode = last_pad-1 //# shifting mode to add kontroller slave in menu
			//Log {MODE:}, mode-1
			in_mode_select = FALSE
		elseif last_pad=5
			StartTimer
			LabelPads {HOLD PAD DOWN TO GENERATE CHORD PROGRESSION FOR SCENE }, current_scene+1
			Exit
		elseif (last_pad>=8 and last_pad<=12)
    	//# midi setup modes - dont exit in_select_mode
    	mode = last_pad
			Call @SetupSettingsLayout
    elseif (last_pad = 14)
			Call @LogCurrentSceneToPresetFormat
			LabelPads {Scene preset data printed. Check log!}
			Exit
		elseif (last_pad = 15) //All Notes off
			mode=last_pad
			Call @AllNotesOff
			LabelPad 15, {Done!}
		else
			mode = last_pad
			//Log {MODE:}, mode
			in_mode_select = FALSE
		endif
		Call @HandleModeChange
		Call @SetupLayout
	//# NOT IN MODE SELECT......
	elseif NOT in_mode_select
		//# handle mode actions if record or reset else do knob scene change
		if not (mode=7 or copy_mode=2)
			Call @OnPadDown_SceneChordChange //dont change if deleting or copy/pasting
		endif	
	 	Call @OnPadDown_KnobSetSelect
		if (mode = 3) and (last_pad > 7)
			Call @SetupLayout
	  	rec_chord_note_count = 0
  		LabelPad current_chord+8, {...waiting}
  		ColorPad current_chord+8, 1
			if mgen_device_id=mgen_sel_dev
				SendMIDINoteOn 15, current_chord+8+led_index, 1 //LED FEEDBACK
			endif
		elseif (mode = 6) //# COPY/PASTE
    	//# only call CP if pad and copy mode correspond
    	if copy_mode=0 and last_pad<=7
	    	Call @CopyPaste //# copy paste scene
	    elseif copy_mode=1 and last_pad>=8 
	    	Call @CopyPaste //# copy paste chord
	    endif
		elseif (mode=7) and (last_pad <= 7) //# reset mode
    	//Log {Reset scene called}
    	Call @ResetScene   
 	 	elseif (mode=7) and (last_pad >= 7)
    	//Log {Reset chord called}
    	Call @ResetChord
  	endif
		if NOT HostRunning and (last_pad >= 8)
			chord_slot = (current_scene * 100) + ((current_chord) * 10)
			current_chord_duration = chord_bank[chord_slot + dur_slot]
			chord_formed = curr_chord_notes[0]>=0 and curr_chord_notes[1]>=0 and curr_chord_notes[2]>=0
			if current_chord_duration>0
				for ch = active_ch_min to active_ch_max
					if midi_chan_modes[ch]=2 or midi_chan_modes[ch]=3  //send chords to channels set up with mode chords
						// can only have one chord note per channel playing at any one time, so make sure turned off first
						chord_note_ch = ch
						Call @TurnOffChordNotes 
						for n = 0 to 3  
							if curr_chord_notes[n] > 0
								SendMIDINoteOn ch, curr_chord_notes[n], 100
								SetNoteState ch, n, curr_chord_notes[n]
							endif
						endfor
					endif
				endfor
			endif
  	endif
	endif
@End			

@AllNotesOff //send midi note off for all channels - handle stuck notes
	for ch = 0 to 15
		Call @SendMidiOffForCh
	endfor
@End

@SendMidiOffForCh //send midi note off for a channel (ch) - handle stuck notes
	for n = 0 to 127
		SendMIDINoteOff ch, n, 64, (n*5) //handle stuck notes with 5ms delay on send
	endfor
	Log {Notes off for ch:}, ch
@End

@OnPadUp
	last_pad=LastPad
  Call @OnPadUp2
@End

@OnPadUp2
	pad_is_down=FALSE
	if NOT HostRunning and (last_pad >= 8)
		current_chord_duration = chord_bank[chord_slot + dur_slot]
		if current_chord_duration>0
			for i = active_ch_min to active_ch_max
				if midi_chan_modes[i]=2 or midi_chan_modes[i]=3
					chord_note_ch = i
	  			Call @TurnOffChordNotes
				endif
			endfor
		endif
  endif
	StopTimer
@End

@HandleModeChange
	//# Handle Labels
	if mode = 0
		LabelPads {[ KORDS ]  Scene: }, current_scene+1, { Chord: }, current_chord+1 
		Call @SetupKnobset1
		songmode = 0
	elseif mode = 1
		LabelPads {+CHORDS SELECT: Select chord, THEN set root and type}
		songmode = 2 //# Lock to chord
		Call @SetupKnobset2
	elseif mode = 2
		LabelPads {+CHORDS CONSTRUCT: Use knobs to build chord}
		songmode = 2 //# Lock to chord
		Call @SetupKnobset6
	elseif mode = 3
		LabelPads {+CHORDS RECORD: Select record channel, chord pad, then play chord notes}
		songmode = 2 //# Lock to chord
		Call @SetupKnobset1
	elseif mode = 6
		//# Copy/Paste pad hit so 'copy' current scene or chord
		//# in_select_mode = FALSE its already been exited
		if copy_mode=0 //# scene
			copied_scene = current_scene
			//Log {Copied current s}, current_scene+1
			LabelPads {Current scene copied. Select a scene to paste to}
		elseif copy_mode=1 //# chord
			copied_scene = current_scene //# just for labelling
			copied_chord = current_chord
			//Log {Copied current chord }, current_chord+1
			LabelPads {Current chord copied. Select a scene, then a chord slot to paste to}
		endif
	elseif mode = 7
		LabelPads {DELETE: Select a scene or chord slot to reset}
	elseif mode>=8 and mode<=11
		if NOT HostRunning
			LabelPads {MIDI INPUT Scale: Maps scale degrees IN to chord notes OUT}
			Call @SetupKnobset5
		endif
	elseif mode=9 
		LabelPads {CHORD NOTE TRIGGERS: Listed notes trigger chord Root, 3rd, 5th 7th in octave and midi channel sent}
		Call @LabelChordTriggerModes 
		Call @SetupKnobset5
	elseif mode=10
		LabelPads {CHORD NOTE TRIGGERS: Listed notes are not part of chord but form part of chord scale. Degrees 2,4,& 6}
		Call @LabelChordTriggerModes 
		Call @SetupKnobset5
	elseif mode=11
		LabelPads {CHORD NOTE TRIGGERS: Not part of chord or scale. Toggle round2chord or use as passing notes only}
		Call @LabelChordTriggerModes 
		Call @SetupKnobset5
	elseif mode = 12
		Call @SetupKnobset3
	endif
	Call @SendMidiCCBackToKnobs
@End

@LabelChordTriggerModes
	CopyArray midi_in_scale_notes, ms, 8
	if in_mode_select
		LabelPad 9,  (NoteName ms[0]), (NoteName ms[2]), (NoteName ms[4]),(NoteName ms[6]),{ _________ },{Chord notes R-3-5-7}
		LabelPad 10, (NoteName ms[1]), (NoteName ms[3]), (NoteName ms[5]),{ _________ },  {Non-Chord scale degs}
		if apn_adj
			LabelPad 11, { REST   }, { ________ }, {Non-Scale    Allow}
			ColorPad 11, 1
		else
		  LabelPad 11, { REST   } ,{ ________ }, {Non-Scale    Round2Chord}
			ColorPad 11, 2
		endif
	endif
@End

@OnPadDown_SceneChordChange
	//# handle scene/chord change xopds
  if (last_pad >= 0 and last_pad <= 7)
  	//# Scene change
  	scene_change_requested = last_pad
		songmode=1 //locked to scene. User pressed scene pad
  	if NOT HostRunning //# immediate change else newbeat/bar handles
  		Call @HandleSceneChange 
  	endif
	elseif (last_pad >= 8 and last_pad <= 15) 	
  	//# User Chord change 
  	chord_change_requested = last_pad - 8 //# chrd 0-7
  	songmode=2 //lockt to chord. User pressed chord pad
		if NOT HostRunning //# immediate change else newbeat/bar handles
  		Call @HandleChordChange 
  	endif
  endif
	Call @SetupLayout	
@End

@OnPadDown_KnobSetSelect
	//# choose a knob set based on pad type
	if last_pad <= 7 and (NOT in_mode_select)
		if (mode = 1)	 //# Select quick chords 2 knobs
			Call @SetupKnobset2
		elseif (mode = 2) //# Construct chords 4 knobs
			Call @SetupKnobset6
		else
			Call @SetupKnobset0
		endif
	elseif last_pad >= 8 and NOT in_mode_select
		if (mode = 1)	
			Call @SetupKnobset2
		elseif (mode = 2) //# Construct chords 4 knobs
			Call @SetupKnobset6
		else
			Call @SetupKnobset1
		endif
	elseif in_mode_select
	  Call @SetupKnobset99 //settings knobset
	endif
	Call @SendMidiCCBackToKnobs
@End

@SendMidiCCBackToKnobs //# Update controller knob vals with midi cc
	//send LED feedback when mgen device is active device to mgen knobs
	if send_cc_back_to_knobs and mgen_device_id=mgen_sel_dev
		SendMidiCC knobs_controller[0], knobs_controller[1], (Round GetKnobValue 0)
		SendMidiCC knobs_controller[0], knobs_controller[2], (Round GetKnobValue 1), 25
		SendMidiCC knobs_controller[0], knobs_controller[3], (Round GetKnobValue 2), 50
		SendMidiCC knobs_controller[0], knobs_controller[4], (Round GetKnobValue 3), 75
	endif
@End

@OnMidiCC
	//# Messages from KONTROLLER to update scene durs and play status for all instances in the KOMPOSER SUITE
	//Log {sending midi thru: }, MIDIChannel+1, {-}, MIDIByte2, {-}, MIDIByte3
	if (MIDIByte2>=101) and (MIDIByte2<=(101+7)) and (MIDIChannel=15) and MIDIByte3<=32
		//# handle scene duration change
		scene_bank[((MIDIByte2-101) * sc_size)] = MIDIByte3 
		Call @SetupLayout	
	elseif (MIDIByte2=22 and MIDIByte3>0 and MIDIChannel=15 and HostRunning)
  	return_to_song_mode_requested = TRUE //# handle return to song mode requested
  	in_mode_select = FALSE
		songmode = 0
  	mode = 0
		Call @SetupLayout
  	ColorPad current_scene, 6
    ColorPad 8, 3	
		if mgen_device_id=mgen_sel_dev
			SendMIDINoteOn 15, current_scene+led_index, 3
			SendMIDINoteOn 15, 8+led_index, 3
		endif
	elseif (MIDIByte2=22 and MIDIByte3>0 and MIDIChannel=15 and not HostRunning)
		return_to_song_mode_requested = TRUE
		in_mode_select = FALSE //Return to song mode
  	songmode = 0
  	mode = 0
		current_scene=0
		current_chord=0
		Call @SetupLayout
		ColorPad current_scene, 3
    ColorPad current_chord+8, 3
		if mgen_device_id=mgen_sel_dev
			SendMIDINoteOn 15, current_scene+led_index, 3
			SendMIDINoteOn 15, 8+led_index, 3
		endif
	elseif (MIDIByte2>=40 and MIDIByte2<=43) and MIDIChannel=15 //KNTRL Knobs on CH16
		if mgen_sel_dev=mgen_device_id
			last_knob = MIDIByte2-40 //simulate  LastPad action
			SetKnobValue last_knob, MIDIByte3 // change knob position
			Call @OnKnobChange2 //handle that change
		endif
	elseif (MIDIByte2=89 and MIDIChannel=15) //copy paste scene
		in_mode_select = FALSE // return to scenes mode first
  	mode = 0
		if MIDIByte3<=7 //handle copy scene
			copied_scene = MIDIByte3
			LabelPads {Scene }, MIDIByte3+1, { copied. Ready to paste}
		elseif MIDIByte3>=8 and MIDIByte3<=15 //handle scene paste
			LabelPads {Copied Scene }, copied_scene+1, { to Scene }, (MIDIByte3-8)+1
			CopyArray scene_bank[copied_scene * sc_size], scene_bank[(MIDIByte3-8) * sc_size], sc_size
			CopyArray chord_bank[copied_scene * 100], chord_bank[(MIDIByte3-8) * 100], 100
			songmode = 1
			mode=0
			copied_scene = -1 //# reset
			if in_mode_select
				Call @SetupSettingsLayout
			else
				Call @SetupLayout
			endif
		endif
	elseif MIDIByte2>=91 and MIDIByte2<=98 and MIDIByte3>0 and MIDIChannel=15
		mgen_sel_dev=MIDIByte2-91+1 // range 1-9 to match display
		if mgen_sel_dev= mgen_device_id
			Call @OnShiftDown2 //will trigger layout setup for settings and other modes
		endif
	elseif MIDIChannel<=15
	  SendMIDIThru //automation
	endif
@End

@OnMidiInput
	if MIDICommand=0xD0 or MIDICommand=0xE0 //Aftertouch or pitchbend pass through
		SendMIDIThru 
	endif
@End

@OnSysex
  ReceiveSysex sysex_msg
  qk_msg=YES
  for i = 0 to len_header-1 //check header make sure its a QK sysex message
    if sysex_msg[i] <> qk_sysex_header[i]
      qk_msg=NO
    endif
  endfor
  if qk_msg 
    msg_start=len_header+1 //where msg_data starts
		msg_end=SysexSize-1 //where msg_data ends
		qk_msg_type=sysex_msg[len_header] //the message type received
		//handle msg types...
		if qk_msg_type = sx_scene_change
			//# handle scene change requested (ie: PadDown Scene change)
			if songmode=0 and HostRunning and sysex_msg[msg_start]=current_scene //in song mode. just lock to scene
				songmode=1 // lock to scene
			elseif songmode=0 and HostRunning
			  scene_change_requested = sysex_msg[msg_start]
				songmode=1
			elseif songmode>=0 and songmode<=2 and HostRunning
			  scene_change_requested = sysex_msg[msg_start]
				songmode=1
				edit_scene=last_pad
			elseif NOT HostRunning //# immediate change else newbeat/bar handles
				scene_change_requested = sysex_msg[msg_start]
				songmode=1 // lock to scene
				Call @HandleSceneChange 
			endif
			Call @SetupLayout	
		endif
	endif
@End

@SaveSelectedChordPad
	//# save to pad in select mode once chord params chosen on knobs 
	//# chord to be updated
	chord_slot = (current_scene * 100) + ((chord_to_set) * 10)
	
	//# Set fields not updated here. Default if new else saved
	if generate_random_chord
		root_updated = TRUE
		type_updated = TRUE
		inv_updated = TRUE
		rnd_durations=[2,4,4,8,8,12,16]
		chord_bank[chord_slot + dur_slot] = rnd_durations[Random 0,6] //duration
		slct_chord_root=new_root_choices[(Random 0,len_new_root_choices)]
		slct_chord_type=new_chord_type_choices[(Random 0,len_new_type_choices)]
		slct_chord_inversion=Random -1,1
	else
		chrd_duration = 4 //# default value for new chord
		if chord_bank[chord_slot + dur_slot] > 0
			chrd_duration = chord_bank[chord_slot + dur_slot]
		endif
		chord_bank[chord_slot + dur_slot] = chrd_duration
	endif
	
	//# Only update changed fields
	if root_updated or type_updated
		//# Get chord_bank location and intervals for new chord
		chord_root = slct_chord_root
		chord_type_slot = slct_chord_type * 5 //# array saved in groups 5
		chord_3rd = (slct_chord_root + chord_types[chord_type_slot])
		chord_5th = (slct_chord_root + chord_types[chord_type_slot + 1])
		chord_7th = (slct_chord_root + chord_types[chord_type_slot + 2])
		//# A triad not a tetrad
		if chord_types[chord_type_slot + 2] = -1 
			chord_7th = -1
		endif
		//# update chord values in chord_bank
		chord_bank[chord_slot] = chord_root
		chord_bank[chord_slot + 1] = chord_3rd
		chord_bank[chord_slot + 2] = chord_5th
		chord_bank[chord_slot + 3] = chord_7th
		root_updated = FALSE
		type_updated = FALSE
	endif
	if inv_updated
		chrd_inversion = slct_chord_inversion	
		chord_bank[chord_slot + inv_slot] = chrd_inversion
		inv_updated = FALSE
	endif
	chrd_to_label = chord_to_set+8
	Call @LabelChordPad
	Call @UpdateChordsWithDuration
	Call @MakeChordScale
@End

@SaveConstructedChordPad
	//# save to pad in construct mode  			
	chord_slot = (current_scene * 100) + ((chord_to_set) * 10) //# chord to be updated	
	//# Set fields not updated here. Default if new else saved
	if generate_random_chord
		chrd_duration = Random 1,16
		constructed_root=new_root_choices[(Random 0,len_new_root_choices)]
		constructed_3rd=Random 0,3
		constructed_5th=Random 0,2
		constructed_7th=Random 0,4
	else
		chrd_duration = 4 //# default value for new chord
	endif
	if chord_bank[chord_slot + dur_slot] > 0
		chrd_duration = chord_bank[chord_slot + dur_slot]
	endif
	chord_bank[chord_slot + dur_slot] = chrd_duration
	
	chord_3rd = constructed_root + c3rd_intervals[constructed_3rd]
	chord_5th = constructed_root + c5th_intervals[constructed_5th]
	if constructed_7th > 0
		chord_7th = constructed_root + c7th_intervals[constructed_7th]
	else
		chord_7th = -1
	endif
	//# Save notes to chord slot
	chord_bank[chord_slot] = constructed_root
	chord_bank[chord_slot + 1] = chord_3rd
	chord_bank[chord_slot + 2] = chord_5th
	chord_bank[chord_slot + 3] = chord_7th
	//Log {Root: }, (NoteName constructed_root), { 3rd: }, c3rd_intervals[constructed_3rd], { 5th: }, c5th_intervals[constructed_5th], { 7th: }, c7th_intervals[constructed_7th], { Chord notes }, constructed_root, { }, chord_3rd, { }, chord_5th, { }, chord_7th, { 7th: }, chord_bank[chord_slot + 3]
	chrd_to_label = chord_to_set+8
	Call @LabelChordPad
	Call @UpdateChordsWithDuration
	Call @MakeChordScale
@End

@RemakeAllChordScales
	real_current_scene=current_scene
	for s = 0 to 7
		current_scene = s 
		if scene_bank[current_scene * sc_size]>0
			Call @MakeChordScaleForScene		
		endif
	endfor
	current_scene=real_current_scene
@End

@MakeChordScaleForScene
	for i = 0 to 7
		chord_to_set=i //always except in random gen and suggest and make for scene
		chord_slot = (current_scene * 100) + ((chord_to_set) * 10) //# chord to be updated	
		if chord_bank[chord_slot + dur_slot]>0
			Call @MakeChordScale //if there is a duration on that chord
		endif
	endfor
@End

@MakeChordScale
	//create a "chord scale" to make transposing incoming notes to chord notes and chords faster
	chord_slot = (current_scene * 100) + ((chord_to_set) * 10) //# chord to be updated
	chord_scale_slot = (current_scene * 120) + ((chord_to_set) * 15)
	txpose_index = scene_bank[(current_scene * sc_size) + sc_txp_slot]
	scn_txpose = txpose_array[txpose_index] //# scene txpose amount
	
	//We need these 4 notes to make the chord scale in raw octave
	chord_root = chord_bank[chord_slot]
	chord_3rd = chord_bank[chord_slot + 1]
	chord_5th = chord_bank[chord_slot + 2]
	chord_7th = chord_bank[chord_slot + 3]
	Call @GetChordFlavour //chord_flavour is major or minor and influences chord_scale 0=major 1=minor
	Call @MakeChordNotesRawOctave
	//chord scale ignores inversion setting. User inverts using incoming octaves
	chord_scale[0]=chord_root + scn_txpose //root eg:C
	chord_scale[1]=chord_root + scn_txpose + apn_adj //root+1 eg:C#
	chord_scale[2]=chord_root + 2 + scn_txpose  //root+2 eg:D
	chord_scale[3]=chord_3rd + scn_txpose - apn_adj //3rd-1 eg:D#
	chord_scale[4]=chord_3rd + scn_txpose //3rd eg:E
	chord_scale[5]=chord_root + 5 + scn_txpose //3rd+1 eg: F aka 4th
	chord_scale[6]=chord_5th + scn_txpose - apn_adj //5th-1 eg: F#
	chord_scale[7]=chord_5th + scn_txpose //5th eg:G 
	chord_scale[8]=chord_5th + scn_txpose + apn_adj //5th+1 eg:G#
	chord_scale[9]=chord_root + 8 + chord_flavour + scn_txpose //root-3 eg: A. 
	chord_scale[10]=chord_root + 10 + chord_flavour + scn_txpose - apn_adj //root-2 eg: A# 
	if chord_7th>=0
		chord_scale[11]=chord_7th + scn_txpose //7th eg: B
	elseif chord_7th<0 //+10 minor flavor; +10major flavour
		chord_scale[11]=chord_root + 10 + chord_flavour + scn_txpose
	endif
	
	//add chord notes to chord scale post inversion for send out chord notes
	//get lowest and highest notes after inversion
	Call @HandleInversionSetting //# re-define chord notes to inverted order sets inverted_chord_name vars
	if chord_7th<0
		sort_list = [inv_chord_root, inv_chord_3rd, inv_chord_5th]
		len_list = 3
	else
	  sort_list = [inv_chord_root, inv_chord_3rd, inv_chord_5th, inv_chord_7th]
		len_list = 4
	endif
	Call @SortList
	chord_notes_w_inv[chord_slot + 0] = inv_chord_root + scn_txpose //chord root post inversion
	chord_notes_w_inv[chord_slot + 1] = inv_chord_3rd + scn_txpose //chord 3rd post inversion
	chord_notes_w_inv[chord_slot + 2] = inv_chord_5th + scn_txpose //chord 5th post inversion
	if chord_7th>=0
		chord_notes_w_inv[chord_slot + 3] = inv_chord_7th + scn_txpose //chord 7th post inversion
	else
	  chord_notes_w_inv[chord_slot + 3] = chord_7th
	endif
	chord_notes_w_inv[chord_slot + 4] = sort_list[0] //Chord lowest notes
	chord_notes_w_inv[chord_slot + 5] = sort_list[len_list-1] //Chord highest note
	CopyArray chord_notes_w_inv[chord_slot], curr_chord_notes, 6
	CopyArray chord_scale, all_chord_scales[chord_scale_slot], 14
	
	//LOGGING!!!
	cs=[]
	CopyArray chord_scale, cs, 14 //just for logging here
	//Log {--- Made chord scale for s1}, current_scene+1, { chord }, chord_to_set+1, {---}
	//Log {MADE CHORD SCALE: [}, cs[0],{-},cs[1],{-},cs[2],{-},cs[3],{-},cs[4],{-},cs[5],{-},cs[6],{-},cs[7],{-},cs[8],{-},cs[9],{-},cs[1],{-},cs[10],{-},cs[11],{]} 
	//Log {MADE CHORD NOTE W INV: [R:},curr_chord_notes[0],{ 3rd:},curr_chord_notes[1],{ 5th:},curr_chord_notes[2],{  7th:},curr_chord_notes[3],{  LO:},curr_chord_notes[4],{  HI:},curr_chord_notes[5],{]}
	//Log {-----}
@End

@SetChordScale
	//requires chord_to_set - mostly current chord, except when suggest and creating random chord prog
	chord_scale_slot = (current_scene * 120) + ((chord_to_set) * 15)
	chord_slot = (current_scene * 100) + ((chord_to_set) * 10) 
	CopyArray all_chord_scales[chord_scale_slot], current_chord_scale, 14 
	CopyArray chord_notes_w_inv[chord_slot], curr_chord_notes, 6
	//Log {SET CURRCHRD NOTES W INV: [ R:},curr_chord_notes[0],{  3rd:},curr_chord_notes[1],{  5th:},curr_chord_notes[2],{  7th:},curr_chord_notes[3],{  LO:},curr_chord_notes[4],{  HI:},curr_chord_notes[5],{]}
@End

@GetChordFlavour
	chord_flavour=1 //major
	if chord_3rd - chord_root = 3
		chord_flavour=0 //minor
	elseif chord_7th>0 and (chord_7th-chord_root=10) //minor7th
	  chord_flavour=0 //minor
	elseif chord_7th>0 and (chord_7th-chord_root=8) //minor6th
		chord_flavour=0 //minor
	endif
@End

@OnMidiNoteOn
	//# Process incoming midi to be re-harmonized (if not Rec Chords mode)
	//# Log {Midi received on CH: }, MIDIChannel, { note: }, MidiNote
	//# -1Off 0Chord 1Round 2+NonChord 3Bass 4Root 5Third 6Fifth 7thru
	//# Record chords from the channel set 
	if mode=3 and  MIDIChannel=record_notes_channel // records mode 
		Call @HandleRecordNotes
	elseif MIDIChannel=15 and MidiNote>=20 and MIDINote<=36 and mgen_sel_dev=mgen_device_id and mgen_device_id>=0 
		// notes 20-36 on channel 16  used to trigger MGEN pads 0-15
		if MIDINote=36 //SHIFT
			if MIDICommand=0x90 //noteOn	
				Call @OnShiftDown2
			endif
		else
			last_pad = MIDINote-20
			if MIDICommand=0x90 and MIDIByte3>0 //noteOn
				Call @OnPadDown2 //instead of OnPadDown which is only triggered by touch of GUI pads in Mozaic
			elseif MIDICommand=0x90 and MIDIByte3=0
				Call @OnPadUp2
			elseif MIDICommand=0x80 //Note off
			  Call @OnPadUp2
			endif
		endif
	elseif midi_chan_modes[MIDIChannel]=0 //MUTE
		Exit
	elseif midi_chan_modes[MIDIChannel]=1  //HARMONIZE  Notes coming in
		midi_note = MidiNote
		melody_note_ch = MIDIChannel
		note_in_velo = MIDIVelocity
		Call @ConvertToHarmonyNotes //Convert and send the note
		if update_held_notes_on_chord_change
			for i = 0 to (num_mel_voices-1) //for held notes over chord changes
				voice = (MIDIChannel*num_mel_voices)+i //Check if there is a voice available
				if mel_note_voices[voice]=-1 //A free voice has a value of -1
					mel_note_voices[voice] = MidiNote
					mel_note_velos[voice] = MIDIVelocity
					Exit
				endif
			endfor
		endif
	elseif midi_chan_modes[MIDIChannel]=2 //GATED CHORD MODE SEND CHORD NOTES from chord_scale with inv  
		note_in_velo = MIDIVelocity
		chord_note_ch=MIDIChannel
		Call @TurnOffChordNotes // can only have one chord note per channel playing at any one time
		gated_chord_inversion = inversions_for_gated_chord[MIDINote%12] //a number from -4 to 4
		chord_slot = (current_scene * 100) + ((current_chord) * 10)
		if chord_bank[chord_slot + 3] = -1 //ie current_chord is a triad
			//inv_adjs is an array len 4, each cell -12, 0 or +12
			CopyArray triad_inversion_adjustments[(gated_chord_inversion+4)*4], inv_adjs, 4 
		else
		  CopyArray tetrad_inversion_adjustments[(gated_chord_inversion+4)*4], inv_adjs, 4
		endif
		Call @SendOutChordNotes
		if update_held_notes_on_chord_change //held note tracking for update on chord change
			held_chord_notes[chord_note_ch] = MIDINote
			held_chord_oct_adj[chord_note_ch] = 0
		endif
	elseif midi_chan_modes[MIDIChannel]=3 //NOTES OF CHORD
		chord_note_ch=MIDIChannel
		note_in_velo = MIDIVelocity
		midi_note=MIDINote
		Call @TurnOffChordNotes // can only have one chord note per channel playing at any one time
		Call @SendOutChordNotes
		if update_held_notes_on_chord_change //held note tracking for update on chord change
			held_chord_notes[chord_note_ch] = midi_note
			held_chord_oct_adj[chord_note_ch] = 0
		endif
	elseif midi_chan_modes[MIDIChannel]=4 //THRU
		SendMIDIThru 
	endif
@End

@SendOutChordNotes
	//# Send out all 4 chord notes when midi_chan_mode = 2 //CHORD from curr_chord_notes array
	//get notes from the current chord scale
	//curr_chord_notes=[lo, root, 3rd, 5th, 7th, hi] //post inversion, txpose, raw octave
	//Modes 0:Off 1:Harmonize 2:GatedChord 3:*ChordOn 4:*RootOn 5:*5thOn 6:*Lo 7:*Hi 8:OFF
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	CopyArray chord_notes_w_inv[chord_slot], curr_chord_notes, 6 //make sure we have current chord notes
	curr_mode = midi_chan_modes[chord_note_ch]
	if curr_mode=2 //GATED CHORDS use chord_scale to derive a chord & apply inversion based on incoming midi note
		if current_chord_scale[0]>=0 and current_chord_scale[4]>=0 and current_chord_scale[7]>=0 
			//aka chord_formed we can send chord notes
			index_to_chord_notes=[0,4,7,11]
			//inv_adjs passed in from onMidiNoteOn. array len 4, each cell -12, 0 or +12
			num_chrd_notes_to_play=2 // a triad
			if chord_bank[chord_slot + 3] >= 0 //7th note in current chord 
				num_chrd_notes_to_play=3 //a tetrad
			endif
			for i = 0 to num_chrd_notes_to_play  
				note_in_velo = 90
				Call @HumanizeVelocity
				SendMIDINoteOn chord_note_ch, (current_chord_scale[index_to_chord_notes[i]] + inv_adjs[i]), velocity
				SetNoteState chord_note_ch, i, (current_chord_scale[index_to_chord_notes[i]] + inv_adjs[i])
			endfor
		endif
	elseif (curr_mode=3 and midi_note%12=5) //NOTES OF CHORD mode incoming note=F play chord
		if curr_chord_notes[0]>=0 and curr_chord_notes[1]>=0 and curr_chord_notes[2]>=0 
			//aka chord_formed we can send chord notes	
			oct_in=Div midi_note, 12
			oct_out=(oct_in*12)-48
			for i = 0 to 3  
				Call @HumanizeVelocity
				if curr_chord_notes[i] > 0
					SendMIDINoteOn chord_note_ch, curr_chord_notes[i]+oct_out, velocity
					SetNoteState chord_note_ch, i, curr_chord_notes[i]+oct_out
				endif
			endfor
		endif
	elseif curr_mode=3 and (midi_note%12=0 or midi_note%12=2 or midi_note%12=4 or midi_note%12=7 or midi_note%12=9 or midi_note%12=11)  
		//Root(C) 3rd(E) 5th(G) 7th(B) Lo(D) Chord(F) Hi(A)
		
		note_in=midi_note%12
		oct_in=Div midi_note, 12
		oct_out=(oct_in*12)-48
		
		Call @HumanizeVelocity
		chrd_note_to_play=0	
		if note_in=0
			chrd_note_to_play = curr_chord_notes[0] //root
		elseif note_in=4
		  chrd_note_to_play = curr_chord_notes[1] //3rd
		elseif note_in=7
		  chrd_note_to_play = curr_chord_notes[2] //5th
		elseif note_in=11
		  chrd_note_to_play = curr_chord_notes[3] //7th
		elseif note_in=2
		  chrd_note_to_play = curr_chord_notes[4] //lowest
		elseif note_in=9
		  chrd_note_to_play = curr_chord_notes[5] //highest
		endif 
		if chrd_note_to_play > 0
			note_out=oct_out+chrd_note_to_play
			SendMIDINoteOn chord_note_ch, note_out, velocity
			SetNoteState chord_note_ch, 0, note_out
		endif
	endif
@End

@TurnOffChordNotes
	//# channels. requires a ch variable to be set before calling
	for i = 0 to 3
		//# stored in 1st 4 note slots (0-3) on each channel
		if (GetNoteState chord_note_ch, i)
			SendMIDINoteOff chord_note_ch, (GetNoteState chord_note_ch, i), 127, 5
			SetNoteState chord_note_ch, i, FALSE
  	endif
  endfor
@End

@ConvertToHarmonyNotes
	//convert to harmony notes using current chord scale if chord formed and chord has duration
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	current_chord_duration = chord_bank[chord_slot + dur_slot]
	if current_chord_duration>0 and (current_chord_scale[0]>0 and current_chord_scale[4]>0 and current_chord_scale[7]>0)
		note_in=midi_note%12
		oct_in=Div midi_note, 12
		note_out=current_chord_scale[note_in]
		oct_out=(oct_in*12)-48
		note_to_send=oct_out+note_out
		//# save calculated harmony note to locker for note off recall
		SetNoteState melody_note_ch, midi_note, note_to_send
		//# send note out
		Call @HumanizeVelocity
	  SendMIDINoteOn melody_note_ch, note_to_send, velocity 
	endif
@End

@HandleInversionSetting
	//only call from make chord scale where chord_root, chord_3rd, chord_5th, chord_7th are set
	//handle +/- octaves to chord notes and order by fixed R357 or ascending chord notes order
	chrd_inversion = chord_bank[chord_slot + inv_slot]
	inv_adj_amts=[0,0,0,0] //no inversion
	if chrd_inversion = 1
		inv_adj_amts=[12,0,0,0]
	elseif chrd_inversion = 2
		inv_adj_amts=[12,12,0,0]
	elseif chrd_inversion = 3
		inv_adj_amts=[12,12,12,0]
	elseif chrd_inversion = 4
		if chord_7th > -1
			inv_adj_amts=[12,12,12,12]
		else
			inv_adj_amts=[12,12,12,0]
		endif
	elseif chrd_inversion = -1
		if chord_7th > -1
			inv_adj_amts=[0,0,0,-12]
		else
			inv_adj_amts=[0,0,-12,0]
		endif
	elseif chrd_inversion = -2
		if chord_7th > -1
			inv_adj_amts=[0,0,-12,-12]
		else
			inv_adj_amts=[0,-12,-12,0]
		endif
	elseif chrd_inversion = -3
		if chord_7th > -1
			inv_adj_amts=[0,-12,-12,-12]
		else
			inv_adj_amts=[-12,-12,-12,0]
		endif
	elseif chrd_inversion = -4
		if chord_7th > -1
			inv_adj_amts=[-12,-12,-12,-12]
		else
			inv_adj_amts=[-12,-12,-12,0]
		endif
	endif
	
	//# Re-define chord notes based on inverted status
	//# remove octave added above
	inv_chord_root = chord_root + inv_adj_amts[0]
	inv_chord_3rd = chord_3rd + inv_adj_amts[1]
	inv_chord_5th = chord_5th + inv_adj_amts[2]
	inv_chord_7th= chord_7th + inv_adj_amts[3]
@End

@MakeChordNotesRawOctave
	//# Set variabes chord_root, 3rd, 5th and 7th.
	//# updated to remove octave but maintain ascending note order
	chord_root = chord_root % 12
	chord_3rd = chord_3rd % 12
	if chord_root > chord_3rd
		chord_3rd = chord_3rd + 12
	endif
	chord_5th = chord_5th % 12
	if chord_3rd > chord_5th
		chord_5th = chord_5th + 12
	endif
	if chord_7th >= 0
		chord_7th = chord_7th % 12
		if chord_5th > chord_7th
			chord_7th = chord_7th + 12
		endif
		chord_7th = chord_7th + base_starting_note
	else
		chord_7th = -1
	endif
	chord_root = chord_root + base_starting_note
	chord_3rd = chord_3rd + base_starting_note
	chord_5th = chord_5th + base_starting_note	
@End
	
@HumanizeVelocity
	// #Velocity humanization. requires note_in_velo 
  humanize_velocity = 80 // #50-100 Lower = more variation
  dip_velocity_pct = 60 // #50-100 Reduce velocity by %
	humanize_pct = (Random humanize_velocity, 100 ) / 100
	velocity = RoundUp (note_in_velo * humanize_pct)
	velocity_dipped = RoundUp (velocity * dip_velocity_pct/100)
@End

@OnMidiNoteOff  
  // #handle midi notes for harmonizing note OFF
	if MIDIChannel=15 and MidiNote>=20 and MIDINote<=36 and mgen_sel_dev=mgen_device_id and mgen_device_id>=0 
		last_pad = MIDINote-20
		if MIDINote=36 and MIDICommand=0x80 //Note off
			shift_is_down=FALSE //SHIFT
		elseif (MIDICommand=0x90 and MIDIByte3=0) or MIDICommand=0x80 //Note off
			Call @OnPadUp2
		endif
	elseif midi_chan_modes[MIDIChannel]=1 //harmonize mode
		harmony_note = GetNoteState MIDIChannel, MIDINote  
  	SendMIDINoteOff MIDIChannel, harmony_note, MIDIVelocity  
  	SetNoteState MIDIChannel, MIDINote, FALSE
		if update_held_notes_on_chord_change
			for i = 0 to (num_mel_voices-1) //for held notes over chord changes
				voice = (MidiChannel*num_mel_voices)+i
				if mel_note_voices[voice]=MIDINote
					mel_note_voices[voice]=-1
					Exit
				endif
			endfor
		endif
	elseif midi_chan_modes[MIDIChannel]=2 or midi_chan_modes[MIDIChannel]=3
		chord_note_ch=MIDIChannel
		Call @TurnOffChordNotes
		if update_held_notes_on_chord_change
			held_chord_notes[chord_note_ch] = -1
			held_chord_oct_adj[chord_note_ch] = 0
		endif
	elseif midi_chan_modes[MIDIChannel]=4 //thru mode
		SendMIDIThru 
	endif
@End

@HandleRecordNotes
	//Log {HANDLING RECORD NOTES TO CHORD SLOT}
	if (mode = 3) and (rec_chord_note_count <= 3)
  	//# In record mode and chord selected for updating
    //Log {Chord note recieved num: }, rec_chord_note_count
    if rec_chord_note_count = 0
	    //# Recording is happening so reset the current notes
	    chord_slot = (current_scene * 100) + (current_chord * 10)
	    for i = 0 to 3
	    	//Log {RESETTING CHORD SLOT }, chord_slot+i
  	  	chord_bank[chord_slot+i] = -1
    	endfor
    	chord_bank[chord_slot+inv_slot] = 0 //# start new chord with no inv.
    	rec_chord_notes = [-1,-1,-1,-1] //# reset array to empty
    	//Log {Current chord: }, current_chord
    	chrd_to_label = current_chord+8
    	Call @LabelChordPad //# re-label after notes reset
    endif
		
		//# Store recorded chord notes and update chord_bank and label
    rec_chord_notes[rec_chord_note_count] = MIDINote
    CopyArray rec_chord_notes, sort_list
    rec_chord_note_count = rec_chord_note_count + 1
    len_list = rec_chord_note_count
    if len_list > 1
	    Call @SortList
    endif
    CopyArray sort_list, rec_chord_notes
    Call @StoreChordNotes
    chrd_to_label = current_chord+8
    Call @LabelChordPad
    Call @SetupLayout
		Call @UpdateChordsWithDuration
		chord_to_set=current_chord //always except in random gen and suggest
		Call @MakeChordScale
  endif
@End

@StoreChordNotes
	//# Store the recorded notes to the chord slot
  chord_slot = (current_scene * 100) + (current_chord * 10)
  for i = 0 to 3
  	if rec_chord_notes[i] >= 0
	  	chord_bank[chord_slot + i] = rec_chord_notes[i]
	  	//Log {Chord note }, i, { is }, rec_chord_notes[i], { in chord slot}, chord_slot+i
	  else
	  	chord_bank[chord_slot + i] = -1
	  endif
  endfor
	if chord_bank[chord_slot + dur_slot] <= 0 //# Set a default duration
  	chord_bank[chord_slot + dur_slot] = 4
  endif
	Log {NOTES: }, chord_bank[chord_slot], {-}, chord_bank[chord_slot+1], {-}, chord_bank[chord_slot+2], {-}, chord_bank[chord_slot+3]
@End

@ResetScene
  //# Reset scene: Duration, Txpose, Preset, modes for ch's 1-4, out ch's ON1-4, pgm_chg_msg
  scene_bank[last_pad * sc_size] = [-1,6,0,0,0,0,0,0,0,0,0,-1]
  chord_slot = (last_pad * 100)
	for c = 0 to 7
		chord_bank[(current_scene*100) + (c*10)] = [-1, -1, -1, -1, 0, 0, -1]
	endfor
  //reset chord scales
	chord_scale_slot = (current_scene * 120)
	FillArray all_chord_scales[chord_scale_slot], -1, (8*15)
	Call @SetupLayout
	num_scns_with_dur=last_pad-1 //RND only selects scenes before deleted scn
	num_chrds_with_dur[current_scene]=0 //del scene also deletes chords
  //Log {Scene }, last_pad +1, { reset. All chords for scene reset}
@End

@ResetChord
	chord_slot = (current_scene * 100) + ((last_pad - 8) * 10)
  //# 4 chord notes, duration, inv, bass, pattern_msg 
  chord_bank[chord_slot] = [-1,-1,-1,-1,0,0,-1,-1]
	num_chrds_with_dur[current_scene]=last_pad-8-1
	//reset chord scales
	chord_scale_slot = (current_scene * 120) + ((current_chord) * 15)
	FillArray all_chord_scales[chord_scale_slot], -1, 15
  //Log {Chord }, last_pad+1, { reset}
  Call @SetupLayout
@End

@CopyPaste
	//# copy paste scenes and chords
	if last_pad <=7
		//Log {Pasting scene to }, current_scene+1 
		LabelPads {Copied Scene }, copied_scene+1, { to Scene }, last_pad+1
		CopyArray scene_bank[copied_scene * sc_size], scene_bank[last_pad * sc_size], sc_size
		CopyArray chord_bank[copied_scene * 100], chord_bank[last_pad * 100], 100
		//# Exit copy paste
		songmode = 1
		mode=0
		Call @UpdateScenesWithDuration
		Call @UpdateChordsWithDuration
		Call @MakeChordScaleForScene
	elseif last_pad >= 8
		//Log {pasting to chord }, last_pad-8
		LabelPads	{Copied S}, copied_scene+1, { Chord }, copied_chord+1, { to }, {S}, current_scene+1, { Chord }, last_pad-7
		chord_slot_from = (copied_scene * 100) + (copied_chord * 10)
		chord_slot_to = (current_scene * 100) + ((last_pad-8) * 10)
		CopyArray chord_bank[chord_slot_from], chord_bank[chord_slot_to], 10
		//# Exit copy paste
		songmode = 2
		mode=0
		Call @UpdateChordsWithDuration
		chord_to_set=current_chord //always except in random gen and suggest
		Call @MakeChordScale
	endif
	copied_scene = -1 //# reset
	copied_chord = -1
	Call @SetupLayout 
@End

@SuggestChord
	//# Make a chord suggestion based on previous chord. Update current chord slot
	chord_slot = (current_scene * 100) + (current_chord * 10) //# chord to update
	suggested_chord_notes=[]
	n1=0 //prev_root
	n2=4 //prev_3rd
	n3=7 //prev_5th
	n4=11 //prev_7th
	d=4 //duration
	i=0 //inversion
	
	if current_scene>0 and current_chord=0 //# use last chord prev_scene
		for x = 0 to 7
			test_slot = ((current_scene-1) * 100) + (x * 10)
			if chord_bank[chord_slot+dur_slot] > 0 //# chord has duration so use it	
				prev_chord_slot=test_slot
			else
			  prev_chord_slot=test_slot
	  	endif
		endfor
		n1=chord_bank[prev_chord_slot]%12
		n2=chord_bank[prev_chord_slot+1]%12
		n3=chord_bank[prev_chord_slot+2]%12
		n4=chord_bank[prev_chord_slot+3]%12
		d=chord_bank[prev_chord_slot+4]
		i=chord_bank[prev_chord_slot+5]%12
	elseif current_chord>0
		prev_chord_slot = ((current_scene) * 100) + ((current_chord-1) * 10)
		n1=chord_bank[prev_chord_slot]%12
		n2=chord_bank[prev_chord_slot+1]%12
		n3=chord_bank[prev_chord_slot+2]%12
		n4=chord_bank[prev_chord_slot+3]%12
		d=chord_bank[prev_chord_slot+4]
		i=chord_bank[prev_chord_slot+5]%12
	endif
	
	CopyArray current_chord_scale, ccs, 14 //use current chord scale to generate new chord shape
	
	//# chord = [note1, note2, note3, note4, duration, inv, bass]
	chord_suggest_bank[0*10]  = [n1,        n2,         n3,         n4,         d,i,n1]
	chord_suggest_bank[1*10]  = [n1,        (n1+2)%12,  n3,         -1,         d,i,n1] //sus2
	chord_suggest_bank[2*10]  = [n1,        (n1+3)%12,  n3,         -1,         d,i,n1] //minor
	chord_suggest_bank[3*10]  = [n1,        (n1+4)%12,  n3,         -1,         d,i,n1] //major
	chord_suggest_bank[4*10]  = [n1,        (n1+5)%12,  n3,         -1,         d,i,n1] //sus4
	chord_suggest_bank[5*10]  = [n1,        (n1+3)%12,  (n1+7)%12,  (n1+10)%12, d,i,n1] //minor7
	chord_suggest_bank[6*10]  = [n1,        (n1+4)%12,  (n1+7)%12,  (n1+11)%12, d,i,n1] //major7
	chord_suggest_bank[7*10]  = [n1,        (n1+4)%12,  (n1+7)%12,  (n1+10)%12, d,i,n1] //dom7
	chord_suggest_bank[8*10]  = [n2,        (n2+4)%12,  (n2+7)%12,  -1,         d,i,n2] //chords 3rd as root
	chord_suggest_bank[9*10]  = [n2,        (n2+3)%12,  (n2+7)%12,  -1,         d,i,n2]
	chord_suggest_bank[10*10] = [n2,        (n2+4)%12,  (n2+7)%12,  (n2+11)%12, d,i,n2]
	chord_suggest_bank[11*10] = [n2,        (n2+3)%12,  (n2+7)%12,  (n2+10)%12, d,i,n2]
	chord_suggest_bank[12*10] = [n3,        (n3+4)%12,  (n3+7)%12,  -1,         d,i,n3] //chords 5th as root
	chord_suggest_bank[13*10] = [n3,        (n3+3)%12,  (n3+7)%12,  -1,         d,i,n3]
	chord_suggest_bank[14*10] = [n3,        (n3+4)%12,  (n3+7)%12,  (n3+11)%12, d,i,n3]
	chord_suggest_bank[15*10] = [n3,        (n3+3)%12,  (n3+7)%12,  (n3+10)%12, d,i,n3]
	chord_suggest_bank[16*10] = [(n1+9)%12, (n1+12)%12, (n2+12)%12, -1,         d,i,(n1+9)%12] //chords root-3rd 
	
	//# Update the current chord with suggested notes
	CopyArray chord_suggest_bank[chrd_sugg*10], chord_bank[chord_slot], 7
	chrd_to_label = current_chord+8
	Call @LabelChordPad		
@End

@SetupKnobset99
	knob_set = 99
	SetKnobValue 0, TranslateScale mgen_device_id, 0, 9, 0, 127
	SetKnobValue 0, 0
	SetKnobValue 2, TranslateScale rnd_scn_chance, 0, 100, 0, 127
	SetKnobValue 3, TranslateScale rnd_chrd_chance, 0, 100, 0, 127
	Call @LabelSettingsKnobs
@End
@KnobChangeSet99 //settings knobs
	if last_knob = 0 and shift_is_down
	  val = Round TranslateScale (GetKnobValue 0), 0, 127, 0, 9
		if val <> mgen_device_id
			mgen_device_id=val
			if mgen_device_id
				LabelKnob 0, {mgen# }, mgen_device_id
			else
				LabelKnob 0, {mgen# -}
			endif
		endif
	elseif last_knob = 0 and not shift_is_down
	  LabelPads {HOLD SHIFT DOWN TO CHANGE MGEN DEVICE ID!!!!}
	elseif last_knob = 2
	  val = Round TranslateScale (GetKnobValue 2), 0, 127, 0, 100
	  if val <> rnd_scn_chance 	
		  rnd_scn_chance = val
			Call @LabelSettingsKnobs
	  endif
  elseif last_knob = 3
	  val = Round TranslateScale (GetKnobValue 3), 0, 127, 0, 100
	  if val <> rnd_chrd_chance 	
		  rnd_chrd_chance = val
			Call @LabelSettingsKnobs
	  endif
	endif
@End
@LabelSettingsKnobs
	if mgen_device_id
		LabelKnob 0, {mgen# }, mgen_device_id
	else
		LabelKnob 0, {mgen# -}
		endif
	LabelKnob 1, { }
	LabelKnob 2, {RndScn}, rnd_scn_chance
	LabelKnob 3, {RndChrd}, rnd_chrd_chance
	LabelKnobs { }
	LabelPads {KORDS : [ SETTINGS ]}
@End

@SetupKnobset0
	//# Scene settings
	knob_set = 0
	LabelKnobs {Scene }, current_scene+1, { setup}
	scn_duration = scene_bank[current_scene * sc_size]
	scn_txpose = scene_bank[(current_scene * sc_size) + sc_txp_slot]
	scn_preset = scene_bank[(current_scene * sc_size) + sc_preset_slot]
	LabelKnob 0, {Duration}
	LabelKnob 1, {Transpose}
	LabelKnob 2, {Preset }, scn_preset
	if txpose_w_circle_fifths
		LabelKnob 3, {TXP CoF} 
	else
		LabelKnob 3, {TXP ST}
	endif
	SetKnobValue 0, TranslateScale scn_duration, 0, 32, 0, 127
	SetKnobValue 1, TranslateScale scn_txpose, 0, 12, 0, 127
	SetKnobValue 2, TranslateScale scn_preset, 1, num_sc_presets, 0, 127
	SetKnobValue 3, TranslateScale txpose_w_circle_fifths, 0, 1, 0, 127 
@End
@KnobChangeSet0
	//# scene settings
	if last_knob = 0 and pad_is_down
	  scn_duration = Round TranslateScale (GetKnobValue 0), 0, 127, 0, 32
	  if scn_duration <> scene_bank[current_scene * sc_size] 	
		  scene_bank[current_scene * sc_size] = scn_duration
	  	LabelKnob 0 , {Dur }, scn_duration
			Call @UpdateScenesWithDuration
	  endif
	elseif last_knob = 0 and NOT pad_is_down
		LabelPads {Normally controlled by Scene Master. Hold PAD DOWN to change.}
  endif
  if last_knob = 1
	  scn_txpose = Round TranslateScale (GetKnobValue 1), 0, 127, 0, 12
	  if scn_txpose <> scene_bank[(current_scene * sc_size) + sc_txp_slot] 	
		  scene_bank[(current_scene * sc_size) + sc_txp_slot] = scn_txpose
		  Call @SetupLayout //# re-label all chord pads
			Call @MakeChordScaleForScene
		endif
  endif
  if last_knob = 2
	  scn_preset = Round TranslateScale (GetKnobValue 2), 0, 127, 1, num_sc_presets
	  scene_bank[(current_scene * sc_size) + sc_preset_slot] = scn_preset
	  LabelKnob 2, scn_preset
  	Call @ChangeScenePreset
		Call @UpdateScenesWithDuration
  endif
  if last_knob = 3
	  txpose_val = Round TranslateScale (GetKnobValue 3), 0, 127, 0, 1
		if txpose_val <> txpose_w_circle_fifths
			txpose_w_circle_fifths = txpose_val
			Call @TransposeCalcArrays
			Call @SetupLayout
			Call @SetupKnobset0
		endif
  endif
  scn_to_label = current_scene
  Call @LabelScenePad
@End

@SetupKnobset1
	//# Chord settings
	knob_set = 1
	LabelKnobs {Chord }, current_chord+1, { setup}
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	chrd_duration = chord_bank[chord_slot + dur_slot]
	chrd_inversion = chord_bank[chord_slot + inv_slot]
	chrd_sugg = chord_bank[chord_slot + sugg_slot]
	if mode = 3
		LabelKnob 1, {REC CH }, record_notes_channel + 1
		SetKnobValue 1, TranslateScale record_notes_channel, 0, 14, 0, 127
	else
		LabelKnob 1, {Suggest}, chrd_sugg
		SetKnobValue 1, TranslateScale chrd_sugg, 0, 16, 0, 127
	endif
	LabelKnob 0, {Duration}	
	LabelKnob 2, {Inv }, chrd_inversion
	LabelKnob 3, { }
	SetKnobValue 0, TranslateScale chrd_duration, 0, 31, 0, 127
	SetKnobValue 2, TranslateScale chrd_inversion, -4, 4, 0, 127
	SetKnobValue 3, 0
@End
@KnobChangeSet1
//# chord settings
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	chord_to_set=current_chord
	if last_knob = 0
	  chrd_duration = (Round TranslateScale (GetKnobValue 0), 0, 127, 0, 31)+1
	  if chrd_duration <> chord_bank[chord_slot + dur_slot] 
		  chord_bank[chord_slot + dur_slot] = chrd_duration
		  dur_bars = Div chrd_duration, 4
			dur_beats = (chrd_duration % HostBeatsPerMeasure) * (100/HostBeatsPerMeasure)
	  	LabelKnob 0 , {Dur }, dur_bars, {.}, dur_beats
			Call @UpdateChordsWithDuration
  	endif
  endif
  if (mode = 3)
	  if last_knob = 1
		  val = Round TranslateScale (GetKnobValue 1), 0, 127, 0, 14
			if val <> record_notes_channel
				record_notes_channel = val
				LabelKnob 1, {REC CH }, record_notes_channel + 1
			endif
	  endif
	else
		if last_knob = 1
		  chrd_sugg = Round TranslateScale (GetKnobValue 1), 0, 127, 0, 16
			if chrd_sugg <> chord_bank[chord_slot + sugg_slot] 
		  	chord_bank[chord_slot + sugg_slot] = chrd_sugg
		  	LabelKnob 1, {Suggest}, chrd_sugg
	  		Call @SuggestChord
				Call @MakeChordScale
			endif
	  endif
  endif
  if last_knob = 2
	  chrd_inversion = Round TranslateScale (GetKnobValue 2), 0, 127, -4, 4
	  if chrd_inversion <> chord_bank[chord_slot + inv_slot] 
		  chord_bank[chord_slot + inv_slot] = chrd_inversion
		  LabelKnob 2, {Inv }, chrd_inversion
			Call @MakeChordScale
	  endif 
  endif
  //# Knob 1 not used in this set
  chrd_to_label = current_chord+8
  Call @LabelChordPad
@End
@UpdateScenesWithDuration
	for s = 0 to 7 //Update number of scenes with duration for random selection
		if scene_bank[s * sc_size] > 0
			num_scns_with_dur=s
		else
		  Exit
		endif	
	endfor
@End
@UpdateChordsWithDuration
	for c = 0 to 7 //Update number of chords in scene with duration for random selection
		if chord_bank[(current_scene * 100) + (c * 10) + dur_slot] > 0
			num_chrds_with_dur[current_scene]=c
		else
		  Exit
		endif	
	endfor
@End

@SetupKnobset2
	//# Chord select settings
	knob_set = 2
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	LabelKnobs {Selecting }, {S}, current_scene+1, { Chord: }, current_chord+1
	
	//# if new chord default values from FillArray used here
	slct_chord_root = chord_bank[chord_slot] % 12
	slct_chord_inversion = chord_bank[chord_slot + inv_slot]
	
	//# Knob setup
	LabelKnob 0, {Root }, (NoteName slct_chord_root)
	LabelKnob 1, {Type}
	LabelKnob 2, {Inv }, slct_chord_inversion 
	LabelKnob 3, { }
	SetKnobValue 0, TranslateScale slct_chord_root, 0, 11, 0, 127
	SetKnobValue 1, TranslateScale slct_chord_type, 0, num_chord_types-1, 0, 127
	SetKnobValue 2, TranslateScale slct_chord_inversion, -4, 4, 0, 127
	SetKnobValue 3, 0
	//# prevent updating values not changed
	root_updated = FALSE
	type_updated = FALSE
	inv_updated = FALSE
@End
@KnobChangeSet2
	inv_updated = FALSE
	chord_to_set = current_chord
	if last_knob = 0
	  val = Round TranslateScale (GetKnobValue 0), 0, 127, 0, 11
  	if val <> slct_chord_root
			slct_chord_root = val
			LabelKnob last_knob, {Root }, (NoteName slct_chord_root)
  		root_updated = TRUE
			Call @SaveSelectedChordPad
		endif
  elseif last_knob = 1
  	val = Round TranslateScale (GetKnobValue 1), 0, 127, 0, num_chord_types-1
		if val <> slct_chord_type
			slct_chord_type = val
	  	if slct_chord_type = 0
	  		LabelKnob 1, {Major}
	  	elseif slct_chord_type = 1
	  		LabelKnob 1, {Minor}
	  	elseif slct_chord_type = 2
	  		LabelKnob 1, {Diminished}
	  	elseif slct_chord_type = 3
	  		LabelKnob 1, {Augmented}
	  	elseif slct_chord_type = 4
	  		LabelKnob 1, {Sus2}
	  	elseif slct_chord_type = 5
	  		LabelKnob 1, {Sus4}
	  	elseif slct_chord_type = 6
	  		LabelKnob 1, {Minor 6th}
	  	elseif slct_chord_type = 7
	  		LabelKnob 1, {Major 6th}
	  	elseif slct_chord_type = 8
	  		LabelKnob 1, {Minor 7th}
	  	elseif slct_chord_type = 9
	  		LabelKnob 1, {Dom 7th}
	  	elseif slct_chord_type = 10
	  		LabelKnob 1, {Major 7th}
	  	endif
	  	type_updated = TRUE
	  	Call @SaveSelectedChordPad
		endif
  elseif last_knob = 2
  	val = Round TranslateScale (GetKnobValue 2), 0, 127, -4, 4
	  if val <> slct_chord_inversion
			slct_chord_inversion = val
			LabelKnob last_knob, {Inv: }, slct_chord_inversion
	  	inv_updated = TRUE
	  	Call @SaveSelectedChordPad
		endif
	endif
@End

@SetupKnobset5
	//# midi in key and scale
	knob_set = 5
	LabelKnobs {Midi IN scale}
	LabelKnob 0, {Key }, (NoteName selected_midi_in_root)
	LabelKnob 1, (ScaleName allowed_scales[selected_midi_in_scale])
	if apn_adj
		LabelKnob 2, {Allow}
	else
	  LabelKnob 2, {Round}
	endif
	LabelKnob 3, { }
	SetKnobValue 0, TranslateScale selected_midi_in_root, 0, 11, 0, 127
	SetKnobValue 1, TranslateScale selected_midi_in_scale, 0, num_scale_types-1, 0, 127
	SetKnobValue 2, TranslateScale apn_adj, 0, 1, 0, 127
	SetKnobValue 3, 0
@End
@KnobChangeSet5
	//# Midi in scale
	if last_knob = 0
	  val = Round TranslateScale (GetKnobValue 0), 0, 127, 0, 11
		if val <> selected_midi_in_root
			selected_midi_in_root = val
			LabelKnob last_knob, (NoteName selected_midi_in_root)
		  Call @GetMidiInScale
			Call @LabelChordTriggerModes
		endif
  elseif last_knob = 1
	  val = Round TranslateScale (GetKnobValue 1), 0, 127, 0, num_scale_types-1
	  if val <> selected_midi_in_scale
			selected_midi_in_scale = val
			LabelKnob last_knob, (ScaleName allowed_scales[selected_midi_in_scale])
		  Call @GetMidiInScale
			Call @LabelChordTriggerModes
		endif
  elseif last_knob = 2
	  val = Round TranslateScale (GetKnobValue 2), 0, 127, 0, 1
		if val <> apn_adj
			apn_adj = val
			Call @RemakeAllChordScales
			if apn_adj
				LabelKnob 2, {Allow}
			else
			  LabelKnob 2, {Round}
			endif	
			Call @LabelChordTriggerModes
		endif
	endif
  LabelPad 8, {  MIDI INPUT  }, {   Key/Scale      }, (NoteName selected_midi_in_root), { }, (ScaleName allowed_scales[selected_midi_in_scale])
@End

@SetupKnobset6
	//# Chord construct settings
	knob_set = 6
	chord_slot = (current_scene * 100) + ((current_chord) * 10)
	LabelKnobs {Construct }, {S}, current_scene+1, { Chord: }, current_chord+1
	
	//# if new chord default values from FillArray used here
	constructed_root = chord_bank[chord_slot] % 12
	root_3rd_interval = chord_bank[chord_slot+1] - chord_bank[chord_slot]
	root_5th_interval = chord_bank[chord_slot+2] - chord_bank[chord_slot]
	root_7th_interval = chord_bank[chord_slot+3] - chord_bank[chord_slot]
	
	//# work out what knob value to assign knobs on selection on new chord pad
	if root_3rd_interval = 3
		constructed_3rd = 1
	elseif (root_3rd_interval = 4)
		constructed_3rd = 2
	elseif (root_3rd_interval > 4)
		constructed_3rd = 3
	else
		constructed_3rd = 0
	endif
	
	if root_5th_interval > 7
		constructed_5th = 2
	elseif (root_5th_interval < 7)
		constructed_5th = 0
	else
		constructed_5th = 1
	endif
	
	if root_7th_interval = 8
		constructed_7th = 1
	elseif (root_7th_interval = 9)
		constructed_7th = 2
	elseif (root_7th_interval = 10)
		constructed_7th = 3
	elseif (root_7th_interval = 11)
		constructed_7th = 4
	elseif (chord_bank[chord_slot+3] < 0)
		constructed_7th = 0
	else
		constructed_7th = 0
	endif
	
	//# Knob setup - Labelling
	LabelKnob 0, {Root }, (NoteName constructed_root)
	Call @LabelConstructedChord
	SetKnobValue 0, TranslateScale constructed_root, 0, 11, 0, 127
	SetKnobValue 1, TranslateScale constructed_3rd, 0, 3, 0, 127
	SetKnobValue 2, TranslateScale constructed_5th, 0, 2, 0, 127
	SetKnobValue 3, TranslateScale constructed_7th, 0, 4, 0, 127
@End
@KnobChangeSet6
	chord_to_set = current_chord //required for @SaveConstructedChordPad
	if last_knob = 0
	  val = Round TranslateScale (GetKnobValue 0), 0, 127, 0, 11
  	if val <> constructed_root
			constructed_root = val
			LabelKnob 0, {Root }, (NoteName constructed_root)
  		Call @SaveConstructedChordPad
		endif
  elseif last_knob = 1
  	val = Round TranslateScale (GetKnobValue 1), 0, 127, 0, 3
  	if val <> constructed_3rd
			constructed_3rd = val
			Call @LabelConstructedChord
  		Call @SaveConstructedChordPad
		endif
  elseif last_knob = 2
  	val = Round TranslateScale (GetKnobValue 2), 0, 127, 0, 2
	  if val <> constructed_5th
			constructed_5th = val
			Call @LabelConstructedChord
	  	Call @SaveConstructedChordPad
		endif
  elseif last_knob = 3
	  val = Round TranslateScale (GetKnobValue 3), 0, 127, 0, 4
  	if val <> constructed_7th
			constructed_7th = val
			Call @LabelConstructedChord
  		Call @SaveConstructedChordPad
		endif	
	endif
@End
@LabelConstructedChord
	if constructed_3rd = 0
		LabelKnob 1, {sus2}
	elseif constructed_3rd = 1
		LabelKnob 1, {min3 (b3)}
	elseif constructed_3rd = 2
		LabelKnob 1, {maj3}
	elseif constructed_3rd = 3
		LabelKnob 1, {sus4}
	endif
	if constructed_5th = 0
  	LabelKnob 2, {dim (b5)}
  elseif constructed_5th = 1
  	LabelKnob 2, {P5}
  elseif constructed_5th = 2
  	LabelKnob 2, {Aug (#5)}
  endif
	if constructed_7th = 0
  	LabelKnob 3, {None}
  elseif constructed_7th = 1
  	LabelKnob 3, {Min6 (b6)}
 	elseif constructed_7th = 2
  	LabelKnob 3, {Maj6}
  elseif constructed_7th = 3
 		LabelKnob 3, {Min7 (b7)}
 	elseif constructed_7th = 4
 		LabelKnob 3, {Maj7}
  endif
@End

@SetupKnobset3
	//Midi channel modes - MUTE-HARMONIZE-CHORD-THRU
	knob_set = 3
	SetKnobValue 0, TranslateScale mode_midi_channel, active_ch_min, active_ch_max, 0, 127
	SetKnobValue 1, TranslateScale midi_chan_modes[mode_midi_channel], 0, 4, 0, 127
	SetKnobValue 2, TranslateScale set_all_mc_to, 0, 5, 0, 127
	SetKnobValue 3, TranslateScale active_ch_range, 0, 4, 0, 127
	mode_changed_for_ch=FALSE
	Call @LabelKnobset3
@End
@KnobChangeSet3
	//Midi channel modes - MUTE-HARMONIZE-CHORD-THRU
	if last_knob = 0
	  val = Round TranslateScale (GetKnobValue 0), 0, 127, active_ch_min, active_ch_max
  	if val <> mode_midi_channel
			mode_midi_channel = val
			Call @LabelKnobset3
			SetKnobValue 1, TranslateScale midi_chan_modes[mode_midi_channel], 0, 4, 0, 127
		endif
  elseif last_knob = 1
  	val = Round TranslateScale (GetKnobValue 1), 0, 127, 0, 4
  	if val <> midi_chan_modes[mode_midi_channel]
			midi_chan_modes[mode_midi_channel] = val
			mode_changed_for_ch=TRUE
			Call @LabelKnobset3
			ch=mode_midi_channel 
			Call @SendMidiOffForCh //send off notes to avoid stuck notes on new mode
		endif
	elseif last_knob = 2
  	val = Round TranslateScale (GetKnobValue 2), 0, 127, 0, 5
  	if val <> set_all_mc_to
			set_all_mc_to = val
			my_fave_modes=[0,0,0,0, 1,2,1,2, 2,3,3,3, 0,0,0,0]
			for i = 0 to 14
				if i>=active_ch_min and i<=active_ch_max
					if set_all_mc_to=5
						midi_chan_modes[i]=my_fave_modes[i]
					else
					  midi_chan_modes[i]=set_all_mc_to
					endif
				endif
			endfor
			SetKnobValue 1, TranslateScale midi_chan_modes[mode_midi_channel], 0, 4, 0, 127
			mode_changed_for_ch=TRUE
			Call @AllNotesOff
			Call @LabelKnobset3
		endif
	elseif last_knob = 3
  	val = Round TranslateScale (GetKnobValue 3), 0, 127, 0, 4
  	if val <> active_ch_range
			active_ch_range = val
			if active_ch_range=0 //active 0-3
				active_ch_min=0
				active_ch_max=3
			elseif active_ch_range=1 //active 0-7
				active_ch_min=0
				active_ch_max=7
			elseif active_ch_range=2 //active 4-11
				active_ch_min=4
				active_ch_max=11
			elseif active_ch_range=3 //active 0-11
				active_ch_min=0
				active_ch_max=11
			elseif active_ch_range=4 //active 0-15
				active_ch_min=0
				active_ch_max=15	
			endif
			for i = 0 to 15
				if i<active_ch_min
					midi_chan_modes[i]=0
					ch=i
					Call @SendMidiOffForCh
				elseif i>active_ch_max
					midi_chan_modes[i]=0
					ch=i
					Call @SendMidiOffForCh
				endif
			endfor
			mode_midi_channel=active_ch_min
			Call @SetupKnobset3
		endif
	endif
@End
@LabelKnobset3
	//Midi channel modes - MUTE-HARMONIZE-CHORD-THRU
	LabelKnobs {MIDI CH MODES}
	LabelKnob 0, {Chan# }, mode_midi_channel+1
	if midi_chan_modes[mode_midi_channel] = 0
		LabelKnob 1, {Off}
		LabelPads {MODE 0: No midi sent thru to instrument}
	elseif midi_chan_modes[mode_midi_channel] = 1
		LabelKnob 1, {Harm'ze}
		LabelPads {MODE 1: Incoming notes for channel are transposed to chord notes}
	elseif midi_chan_modes[mode_midi_channel] = 2
		LabelKnob 1, {Chord}
		LabelPads {MODE 2: Note in triggers current chord. C=root. Invert chord up or down from C}
	elseif midi_chan_modes[mode_midi_channel] = 3
		LabelKnob 1, {ChdNotes}
		LabelPads {MODE 3: C Major notes trigger current chord. Root(C) 3rd(E) 5th(G) 7th(B) Lo(D) Chord(F) Hi(A)}
	elseif midi_chan_modes[mode_midi_channel] = 4
		LabelKnob 1, {Thru}
		LabelPads {MODE 8: Midi passes thru without transposition}
	endif
	LabelKnob 2, {Set All:}, set_all_mc_to
	if active_ch_range=0
		LabelKnob 3, {Active 1-4}
	elseif active_ch_range=1
		LabelKnob 3, {Active 1-8}
	elseif active_ch_range=2
		LabelKnob 3, {Active 5-12}
	elseif active_ch_range=3
		LabelKnob 3, {Active 1-12}
	elseif active_ch_range=4
		LabelKnob 3, {Active 1-15}
	endif
	Call @LabelMidiChModesSettingsPad
	if not mode_changed_for_ch
		LabelPads {MODES: [1-3:Send note on to trigger]   [0:Mute 4:Thru]}
		mode_changed_for_ch=FALSE
	endif
@End

@LabelMidiChModesSettingsPad
	CopyArray midi_chan_modes, m, 16
	if in_mode_select and active_ch_range=0 //0-3
		LabelPad 12, {[}, m[0],{-}, m[1],{-}, m[2],{-}, m[3],{]  }, {   [X-X-X-X]  },  {  [X-X-X-X] }, {  [X-X-X-X] } 			
	elseif in_mode_select and active_ch_range=1 //0-7
		LabelPad 12, {[}, m[0],{-}, m[1],{-}, m[2],{-}, m[3],{]  }, { [}, m[4],{-}, m[5],{-}, m[6],{-}, m[7],{]  }, {  [X-X-X-X] }, {  [X-X-X-X] }
	elseif in_mode_select and active_ch_range=2 //4-11
		LabelPad 12, {[X-X-X-X]  }, {[}, m[4],{-}, m[5],{-}, m[6],{-}, m[7],{] },  { [}, m[8],{-}, m[9],{-}, m[10],{-}, m[11],{]  }, {  [X-X-X-X]}
	elseif in_mode_select and active_ch_range=3 //0-11
		LabelPad 12, {[}, m[0],{-}, m[1],{-}, m[2],{-}, m[3],{]  }, { [}, m[4],{-}, m[5],{-}, m[6],{-}, m[7],{]  },  {  [}, m[8],{-}, m[9],{-}, m[10],{-}, m[11],{]   }, {  [X-X-X-X]}
	elseif in_mode_select and active_ch_range=4
		LabelPad 12, {[}, m[0],{-}, m[1],{-}, m[2],{-}, m[3],{]  }, { [}, m[4],{-}, m[5],{-}, m[6],{-}, m[7],{]  },  {  [}, m[8],{-}, m[9],{-}, m[10],{-}, m[11],{] }, {[}, m[12],{-}, m[13],{-}, m[14],{-}, {X] } 
	endif
@End

@UpdateCurrentKnobset
	if knob_set = 0 
	  Call @SetupKnobset0 //# scene settings
  elseif knob_set = 1
  	Call @SetupKnobset1 //# chord settings
  elseif knob_set = 2
  	Call @SetupKnobset2	//# CHORD select
  elseif knob_set = 3
  	Call @SetupKnobset3	//# MIDI CHANNEL MODES
	elseif knob_set = 5
  	Call @SetupKnobset5	//# midi in key scale
  elseif knob_set = 6
  	Call @SetupKnobset6	//# CHORD construct
	endif
	//# On entering select mode the following need knobsets selected
	if in_mode_select
		if (mode = 9)
			Call @SetupKnobset5
		else
			Call @SetupKnobset99 //settings knobs
		endif
	endif
	Call @SendMidiCCBackToKnobs
@End

@OnKnobChange
  last_knob=LastKnob
	Call @OnKnobChange2
@End

@OnKnobChange2
	if knob_set = 0 
	  Call @KnobChangeSet0 //# scene settings
  elseif knob_set = 1
  	Call @KnobChangeSet1 //# chord settings
  elseif knob_set = 2
  	Call @KnobChangeSet2	//# CHORD select
  elseif knob_set = 3
  	Call @KnobChangeSet3	//# MIDI CH modes
	elseif knob_set = 5
  	Call @KnobChangeSet5	//# midi in scale
  elseif knob_set = 6
  	Call @KnobChangeSet6	//# CHORD construct
	elseif knob_set = 99 //settings layout
		Call @KnobChangeSet99 
	endif 
@End 

@OnShiftDown
  Call @OnShiftDown2 //simply to allow midi note in simulation of SHIFT Note16 any channel
@End	

@OnShiftDown2
	shift_is_down=TRUE
	if in_mode_select
		in_mode_select = FALSE
		LabelPads {[ KORDS ]  Scene: }, current_scene+1, { Chord: }, current_chord+1
		if (mode >= 1)
			mode = 0
			knob_set = 1
		endif		
		Call @SetupLayout
		Call @OnPadDown_KnobSetSelect
	else
		Call @SetupSettingsLayout
	endif
@End

@OnShiftUp
  shift_is_down=FALSE
@End

@SetupSettingsLayout
	// Mode selection for Knobs n Pads
	in_mode_select = TRUE
	LabelPads {_QK:KORDS SETTINGS:   (shift to exit)}
	for i = 0 to (number_of_modes - 1)
		ColorPad i, 6
		LabelPad i, { } //# empty label to overwrite chord info
		LatchPad i, NO 
	endfor
	apn_col=2
	if apn_adj
		apn_col=1
	endif
	settings_pad_cols=[8,4,7,7,7,0,4,1, 2,2,2,apn_col,6,0,3,1]
	if (songmode = 0)
		LabelPad 0, {PLAYSONG: Active }
	elseif (songmode = 1)
		LabelPad 0, {PLAYSONG: Scene locked}
	elseif (songmode = 2)
		LabelPad 0, {PLAYSONG: Chord locked}
	else
		LabelPad 0, {PLAYSONG: Disabled}
	endif	
	LabelPad 1, {SCENES}
	LabelPad 2, {+CHORDS: Select}
	LabelPad 3, {+CHORDS: Construct}
	LabelPad 4, {+CHORDS: Record}
	LabelPad 5, {GENERATE CHORD PROG.}
	if last_pad<=7 //# copy scene or chord
		LabelPad 6, {COPY/PASTE }, {SCENE }, current_scene+1
		copy_mode=0
	else
		LabelPad 6, {COPY/PASTE }, {CHORD }, current_chord+1
		copy_mode=1 
	endif
	LabelPad 7, {DELETE  }
	LabelPad 8, { MIDI INPUT  }, {   Key/Scale      }, (NoteName selected_midi_in_root), { }, (ScaleName allowed_scales[selected_midi_in_scale])
	Call @LabelChordTriggerModes
	Call @LabelMidiChModesSettingsPad
	LabelPad 13,{ }
	LabelPad 14, { SAVE     }
	LabelPad 15, { ALL NOTES OFF }
	if (mode>=1 and mode<=3)
		LatchPad mode+1, YES //#shifted +Chords in menu
		settings_pad_cols[mode+1]=8
	endif
	for i = 0 to (number_of_modes - 1)
		ColorPad i, settings_pad_cols[i]
		if mgen_device_id=mgen_sel_dev
			SendMIDINoteOn 15, i+led_index, settings_pad_cols[i]
		endif
		if settings_pad_cols[i]=8
			LatchPad i, YES
		endif
	endfor
	Call @UpdateCurrentKnobset
@End

@OnTimer
	//generate chord progression for current scene
	generate_random_chord=TRUE
	if current_chord_scale[0]>=0 and current_chord_scale[4]>=0 and current_chord_scale[7]>=0
		new_root_choices = [current_chord_scale[0], current_chord_scale[4], current_chord_scale[5], current_chord_scale[7]]
	else
	  new_root_choices = [0,4,5,7]
	endif
	len_new_root_choices=2
	new_chord_type_choices=[0,1,4,5,6,7,8,9,10]
	len_new_type_choices=8
	for rc = 0 to 7
		chord_to_set=rc //required for SaveContructedChordPad
		//Call @SaveConstructedChordPad
		Call @SaveSelectedChordPad
		Call @MakeChordScale
		Call @SetChordScale //set the chord scale to the newly created chord to help with suggesting next 
		new_root_choices = [current_chord_scale[0], current_chord_scale[4], current_chord_scale[5], current_chord_scale[7]]
	endfor
	in_mode_select = FALSE
	mode = 0
	knob_set = 1
	Call @SetupLayout
	LabelPads {RANDOM CHORD PROGRESSION CREATED FOR SCENE }, current_scene+1
	StopTimer
	generate_random_chord=FALSE
@End

@SortList
  //# set list to sort to sort_list and len_list to # items is your list
  //Log {List to sort }, sort_list[0], sort_list[1], sort_list[2], sort_list[3]
  changed = 1
  while (changed = 1)
    changed = 0
    for i = 0 to len_list - 2
	  if sort_list[i] > sort_list[i+1]
	     changed = 1
        tmp = sort_list[i]
        sort_list[i] = sort_list[i+1]
        sort_list[i+1] = tmp
      endif
    endfor
  endwhile
  //Log {Sorted List: }, sort_list[0], sort_list[1], sort_list[2], sort_list[3]
@End

@LabelScenePad
	//# Set scn_to_label var before calling
	sc_dur = scene_bank[scn_to_label * sc_size]
	sc_txp = txpose_labels[scene_bank[(scn_to_label * sc_size) + sc_txp_slot]]
	sc_preset = scene_bank[(scn_to_label * sc_size) + sc_preset_slot]
	sc_txpm = sc_txp - 3
	if sc_txpm < 0
		sc_txpm = sc_txpm + 12
	endif
	if txpose_w_circle_fifths
		LabelPad scn_to_label, {S}, scn_to_label+1, { [}, sc_dur, { bars]}, {    }, (NoteName sc_txp), {/}, (NoteName sc_txpm), {m(}, txpose_array[scene_bank[(scn_to_label * sc_size) + sc_txp_slot]], {st) }
	else
		LabelPad scn_to_label, {S}, scn_to_label+1, { [}, sc_dur, { bars]}, {    Txpose: }, txpose_array[scene_bank[(scn_to_label * sc_size) + sc_txp_slot]], {st }
	endif
@End

@LabelChordPad
	//# Set chrd_to_label var before calling xlcp
	chord_slot = (current_scene * 100) + ((chrd_to_label-8) * 10)
	//# txpose amount. get value stored on dial use to access array of st amounts
	txpose_index = scene_bank[(current_scene * sc_size) + sc_txp_slot]
	sta = txpose_array[txpose_index] //# scene txpose amount
	cn1 = (chord_bank[chord_slot] + 12 + sta) % 12
	cn2 = (chord_bank[chord_slot + 1] + 12 + sta) % 12
	cn3 = (chord_bank[chord_slot + 2] + 12 + sta) % 12
	if chord_bank[chord_slot + 3] >= 0
		cn4 = (chord_bank[chord_slot + 3] + 12 + sta) % 12
	else
		cn4 = chord_bank[chord_slot + 3] // ie -1 no 7th note
	endif
	cd = chord_bank[chord_slot + dur_slot]
	cd_bars = Div cd, 4
	lc_beats = (cd % HostBeatsPerMeasure) * (100/HostBeatsPerMeasure)
	ci = chord_bank[chord_slot + inv_slot]
	Call @ReorderNoteLabelsForInversion
	if cn4 >= 0 //# A tetrad witha a note i cn4 
		LabelPad chrd_to_label, {[}, cd_bars, {.}, lc_beats, { bars]}, { Inv:}, 	ci, {     Root:}, (NoteName cn1), {      }, (NoteName invo[0]), (NoteName invo[1]), (NoteName invo[2]), (NoteName invo[3])  
	else
		LabelPad chrd_to_label, {[}, cd_bars, {.}, lc_beats, { bars]}, { Inv:}, 	ci, {     Root:}, (NoteName cn1), {      }, (NoteName invo[0]), (NoteName invo[1]), (NoteName invo[2])
	endif
@End

@ReorderNoteLabelsForInversion
	//# Just re-orders the notes on the chord pad in inverted order
	invo = [] //# inverted chord notes
	if (ci = 0) or (ci = 4) or (ci = -4)
		invo = [cn1, cn2, cn3, cn4]
	elseif ci = 1
		invo = [cn2, cn3, cn4, cn1]
		if cn4 = -1
			invo = [cn2, cn3, cn1, cn4]
		endif
	elseif ci = 2
		invo = [cn3, cn4, cn1, cn2]
		if cn4 = -1
			invo = [cn3, cn1, cn2, cn4]
		endif
	elseif ci = 3
		invo = [cn4, cn1, cn2, cn3]
		if cn4 = -1
			invo = [cn1, cn2, cn3, cn4]
		endif
	elseif ci = -1
		invo = [cn4, cn1, cn2, cn3]
		if cn4 = -1
			invo = [cn3, cn1, cn2, cn4]
		endif
	elseif ci = -2
		invo = [cn3, cn4, cn1, cn2]
		if cn4 = -1
			invo = [cn2, cn3, cn1, cn4]
		endif
	elseif ci = -3
		invo = [cn2, cn3, cn4, cn1]
		if cn4 = -1
			invo = [cn1, cn2, cn3, cn4]
		endif
	endif
@End

@SetupLayout
	//#xsl setup layout pads
  if in_mode_select
  	Exit
  else
		//# Setup scene pads
		FillArray scn_pad_cols, 0, 16
		for i = 0 to 7  
	    if scene_bank[i * sc_size] > 0
				scn_pad_cols[i]=2
				scn_to_label = i
				Call @LabelScenePad
	  	else
				scn_pad_cols[i]=0
	  		LabelPad i, { }
	  	endif
	    LatchPad i, NO
	    if (scene_bank[i * sc_size] > 0) and (mode=7) and NOT in_mode_select
				scn_pad_cols[i]=1
	    endif
	  endfor		  
	  //# Setup chord pads  
    for i = 8 to 15
	    chord_slot = (current_scene * 100) + ((i-8) * 10)
	    LatchPad i, NO
	    if chord_bank[chord_slot + dur_slot] > 0
				scn_pad_cols[i]=2
	    	chrd_to_label = i
	    	Call @LabelChordPad
	    else 
				scn_pad_cols[i]=0
	  		LabelPad i, { }
	  	endif
	    if (mode = 3) //del
				scn_pad_cols[i]=1
		  elseif (mode=7) and (chord_bank[chord_slot] >= 0)
				scn_pad_cols[i]=1
		  endif
	  endfor
		//# Handle current scene and chord colors
	  if (songmode = 0) //# Playthru no lock
	  	scn_pad_cols[current_scene]=3
	  	scn_pad_cols[current_chord+8]=3
	  elseif (songmode = 1) //# Lock to scene
	    scn_pad_cols[current_chord+8]=3
			scn_pad_cols[current_scene]=4
	  elseif (songmode = 2)
	    scn_pad_cols[current_chord+8]=4
			scn_pad_cols[current_scene]=4
	  elseif (mode = 3) //# REC CHORDS 
			scn_pad_cols[current_chord+8]=1
			if rec_chord_note_count = 0
				LabelPad current_chord+8, {... waiting}
			endif
		endif
		//# Handle pending chord and scene changes in playback
	  if HostRunning and (songmode <=2)
	  	if chord_change_requested >= 0
				scn_pad_cols[chord_change_requested+8]=6
	  	endif
	  	if scene_change_requested >= 0
	  		scn_pad_cols[scene_change_requested]=6
	  	endif
		endif
	endif
	for i = 0 to 15
		ColorPad i, scn_pad_cols[i]
		if mgen_device_id=mgen_sel_dev
			SendMIDINoteOn 15, i+led_index, scn_pad_cols[i]
		endif
	endfor
@End
	
@TransposeCalcArrays
	if txpose_w_circle_fifths
	  //# Creates an array to transpose using the circle of 5ths 
	  //# but keeping to one octave
	  FillArray txpose_array, 99, 13
	  FillArray txpose_labels, 99, 13 
	  txpose_index = 0
	  for i = 6 to 11
	    key = (i * 7) % 12
	    txpose_val = key
	    if key > Abs (key - 12)
	      txpose_val = key - 12 
	    endif
	    // Log {APos: }, i, { Key: }, (NoteName key), { TXPOSE: }, txpose_val, { Semitones: }, key, { -VE: }, key -12
	    txpose_array[txpose_index] = txpose_val
	    label = txpose_array[txpose_index]
	    if txpose_array[txpose_index] < 0
	      label = txpose_array[txpose_index] + 12
	    endif
	    txpose_labels[txpose_index] = label
	    txpose_index = txpose_index + 1 
	  endfor  
	  for i = 0 to 6
	    key = (i * 7) % 12
	    txpose_val = key
	    if key > Abs (key - 12)
	      txpose_val = key - 12 
	    endif
	    // Log {BPos: }, i, { Key: }, (NoteName key), { TXPOSE: }, txpose_val, { Semitones: }, key, { -VE: }, key -12 
	    txpose_array[txpose_index] = txpose_val
	    label = txpose_array[txpose_index]
	    if txpose_array[txpose_index] < 0
	      label = txpose_array[txpose_index] + 12
	    endif
	    txpose_labels[txpose_index] = label
	    txpose_index = txpose_index + 1
	  endfor
	else
		txpose_array = [-6,-5,-4,-3,-2,-1,0,1,2,3,4,5,6]
	endif
@End

@ChangeScenePreset
	//# Update chords for the current scene with a set of chords
	//# Create a scene in scene_bank
	//# use arrays psd, psn, prog_durs, prog_invs, prog_bass to load data 
	//# use psn array for scale notes, use 0 in front to keep numbering sane
	//# use 1 octave of scale notes. Inversions will be calculated
	//# Call @HandleChordBankPresetUpdate and then LabelPads with scene name
	//# ionian = [2,2,1,2,2,2,1] CDEFGABC [0, 60, 62, 64, 65, 67, 69, 71, 72]
	//# dorian = [2,1,2,2,2,1,2] DEFGABCD [0, 62, 64, 65, 67, 69, 71, 72, 74]
 	//# phrygian = [1,2,2,2,1,2,2] EFGABCDE [0, 64, 65, 67, 69, 71, 72, 74, 76]
	//# lydian = [2,2,2,1,2,2,1] FGABCDEF [0, 65, 67, 69, 71, 72, 74, 76, 77]
	//# mixolydian = [2,2,1,2,2,1,2] GABCDEFG [0, 67, 69, 71, 72, 74, 76, 77, 79]
	//# aeolian = [2,1,2,2,1,2,2,] ABCDEFGA [0, 69, 71, 72, 74, 76, 77, 79, 81]
	//# locrian = [1,2,2,1,2,2,2] BCDEFGAB [0, 71, 72, 74, 76, 77, 79, 81, 83]
	
	//# Default chord bank settings. Copy to preset to override
	//# scene setup. map txpose based on circle of 5ths. 
	//# scn_preset must always be setting for preset (self)
	scene_bank[current_scene * sc_size] = [8,6,scn_preset,1,1,1,1,8,8,8,8,-1]
	
	//# chord settings
	//# psd is scale degrees for each chord in the progression
	psd = [1,3,5,-1, 2,4,6,-1, 3,5,7,-1, 4,6,1,-1, 5,7,2,-1, 6,1,3,-1, 7,2,4,-1, 1,3,5,7]
	//# other settings for the 8 chord slots in this current scene
	prog_durs = [4,4,4,4,4,4,4,4]
	prog_bass = [1,2,3,4,5,6,7,1]
	prog_invs = [0,0,0,0,0,0,0,3]
	//# chord notes always stored in an ascending order. 
	//# So 461 will have an octave added to the 1. 
	//# prog_invs are applied at NoteOut. No effect on how notes stored here
				
	if scn_preset = 1
  	scene_bank[current_scene * sc_size] = [0,6,scn_preset,1,1,1,1,8,8,8,8,-1]
	  chord_bank[(current_scene * 100) + 0]  = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 10] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 20] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 30] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 40] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 50] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 60] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 70] = [-1,-1,-1,-1,0,0,-1]
	 LabelPads {An empty scene. Press shift to select a +CHORDS mode}
	 Call @SetupLayout
	elseif scn_preset = 2
		scene_bank[current_scene * sc_size] = [4,6,scn_preset,1,1,1,1,8,8,8,8,-1]
		psn = [0, 60, 62, 64, 65, 67, 69, 71, 72]
		psd = [1,3,5,-1, 5,7,2,-1, 6,1,3,-1, 4,6,1,-1, -1,-1,-1,-1, -1,-1,-1,-1, -1,-1,-1,-1, -1,-1,-1,-1]
		prog_durs = [4,4,4,4,0,0,0,0]
		prog_bass = [1,5,6,4,0,0,0,0]
		prog_invs = [0,-1,-1,-1,0,0,0,0]
		Call @HandleChordBankPresetUpdate
		chord_bank[(current_scene * 100) + 40] = [-1,-1,-1,-1,0,0,-1] //overwrite with empty
	  chord_bank[(current_scene * 100) + 50] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 60] = [-1,-1,-1,-1,0,0,-1]
	  chord_bank[(current_scene * 100) + 70] = [-1,-1,-1,-1,0,0,-1]
  	LabelPads {Classic I-V-vi-IV progression in C Major}
	elseif scn_preset = 3
		psn = [0, 60, 62, 64, 65, 67, 69, 71, 72]
		prog_invs = [0,0,0,0,0,0,0,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {C IONIAN Major diatonic chord set}
	elseif scn_preset = 4
		psn = [0, 62, 64, 65, 67, 69, 71, 72, 74]
		prog_invs = [0,0,0,0,0,0,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {D DORIAN diatonic chord set}
  elseif scn_preset = 5
		psn = [0, 64, 65, 67, 69, 71, 72, 74, 76]
		prog_invs = [0,0,0,0,0,3,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {E PHRYGIAN diatonic chord set}
  elseif scn_preset = 6
		psn = [0, 65, 67, 69, 71, 72, 74, 76, 77]
		prog_invs = [0,0,0,0,3,3,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {F LYDIAN diatonic chord set}
  elseif scn_preset = 7
		psn = [0, 67, 69, 71, 72, 74, 76, 77, 79]
		prog_invs = [0,0,0,3,3,3,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {G MIXOLYDIAN diatonic chord set}
  elseif scn_preset = 8
		psn = [0, 69, 71, 72, 74, 76, 77, 79, 81]
		prog_invs = [0,0,3,3,3,3,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {A AEOLIAN Natural Minor diatonic chord set}
  elseif scn_preset = 9
		psn = [0, 71, 72, 74, 76, 77, 79, 81, 83]
		prog_invs = [0,3,3,3,3,3,3,4]
		Call @HandleChordBankPresetUpdate
  	LabelPads {B LOCRIAN diatonic chord set}
  elseif scn_preset = 10
	  scene_bank[current_scene * sc_size] = [4,6,10,1,1,1,1,8,8,8,8,-1]
	  chord_bank[(current_scene * 100) + 0] = [9,12,16,-1,4,0,69]
	  chord_bank[(current_scene * 100) + 10] = [11,14,17,-1,4,0,71]
	  chord_bank[(current_scene * 100) + 20] = [0,4,7,-1,4,3,72]
	  chord_bank[(current_scene * 100) + 30] = [2,5,9,-1,4,3,74]
	  chord_bank[(current_scene * 100) + 40] = [4,8,11,-1,4,3,76]
	  chord_bank[(current_scene * 100) + 50] = [5,9,12,-1,4,3,77]
	  chord_bank[(current_scene * 100) + 60] = [7,10,13,-1,4,3,7]
	  chord_bank[(current_scene * 100) + 70] = [9,12,16,-1,4,3,69]
	  LabelPads {Minor triadic harmony (from Natural & Harmonic minor diatonic chords)} 
 	  Call @SetupLayout
	elseif scn_preset = 11
	  scene_bank[current_scene * sc_size] = [4,6,10,1,1,1,1,8,8,8,8,-1]
	  chord_bank[(current_scene * 100) + 0] = [9,12,16,19,4,0,69]
	  chord_bank[(current_scene * 100) + 10] = [11,14,17,21,4,0,71]
	  chord_bank[(current_scene * 100) + 20] = [0,4,7,11,4,4,72]
	  chord_bank[(current_scene * 100) + 30] = [2,5,9,12,4,4,74]
	  chord_bank[(current_scene * 100) + 40] = [4,8,11,14,4,4,76]
	  chord_bank[(current_scene * 100) + 50] = [5,9,12,16,4,4,77]
	  chord_bank[(current_scene * 100) + 60] = [7,10,13,17,4,4,7]
	  chord_bank[(current_scene * 100) + 70] = [9,12,16,19,4,4,69]
	  LabelPads {Diatonic 7th chords Minor (Hybrid)} 
	  Call @SetupLayout
  elseif scn_preset = 12
    scene_bank[current_scene * sc_size] = [4,6,13,1,1,1,1,8,8,8,8,-1]
    chord_bank[(current_scene * 100) + 0] = [0,4,7,-1,4,0,0]
    chord_bank[(current_scene * 100) + 10] = [0,4,7,-1,4,-1,0]
    chord_bank[(current_scene * 100) + 20] = [0,4,7,-1,4,1,0]
    chord_bank[(current_scene * 100) + 30] = [0,3,7,-1,4,0,0]
    chord_bank[(current_scene * 100) + 40] = [0,2,7,-1,4,0,0]
    chord_bank[(current_scene * 100) + 50] = [0,5,7,-1,4,0,0]
    chord_bank[(current_scene * 100) + 60] = [0,4,7,11,4,0,0]
    chord_bank[(current_scene * 100) + 70] = [0,4,7,10,4,0,0]
 		LabelPads {Noodling the tonic - MAJOR} 
 		Call @SetupLayout
  elseif scn_preset = 13
    scene_bank[current_scene * sc_size] = [4,6,13,1,1,1,1,8,8,8,8,-1]
    chord_bank[(current_scene * 100) + 0] = [9,12,16,-1,4,0,9]
    chord_bank[(current_scene * 100) + 10] = [9,12,16,-1,4,-1,9]
    chord_bank[(current_scene * 100) + 20] = [9,12,16,-1,4,1,9]
    chord_bank[(current_scene * 100) + 30] = [9,11,16,-1,4,0,9]
    chord_bank[(current_scene * 100) + 40] = [9,14,16,-1,4,0,9]
    chord_bank[(current_scene * 100) + 50] = [9,12,16,-1,4,0,9]
    chord_bank[(current_scene * 100) + 60] = [9,12,16,19,4,0,9]
    chord_bank[(current_scene * 100) + 70] = [9,13,16,19,4,0,9]
 		LabelPads {Noodling the tonic - MINOR} 
 		Call @SetupLayout
  endif
	Call @MakeChordScaleForScene
@End

@HandleChordBankPresetUpdate
	chord_slot = (current_scene * 100)
	for c = 0 to 7
		//# get from scale note array using scale degree as index
		cn1 = psn[ psd[(c*4) + 0] ]
		cn2 = psn[ psd[(c*4) + 1] ]
		cn3 = psn[ psd[(c*4) + 2] ]
		//# Log {CNs }, cn1, cn2, cn3
		if psd[(c*4) + 3] >= 0 //# 7th note is added
			cn4 = psn[ psd[(c*4) + 3] ]
		else
			cn4 = -1
		endif
		//# return scale degrees to root inversion
		if cn2 < cn1
			cn2 = cn2 + 12
			//# Log {cn2 after inv: }, cn2
		endif
		if cn3 < cn2
			cn3 = cn3 + 12
			//# Log {cn3 after inv: }, cn3
		endif
		if (cn4 >= 0) and (cn4 < cn3)
			cn4 = cn4 + 12
			//# Log {cn4 after inv: }, cn4
		endif
		chord_bank[chord_slot + (c*10) + 0] = cn1
		chord_bank[chord_slot + (c*10) + 1] = cn2
		chord_bank[chord_slot + (c*10) + 2] = cn3
		chord_bank[chord_slot + (c*10) + 3] = cn4
		chord_bank[chord_slot + (c*10) + 4] = prog_durs[c]
		chord_bank[chord_slot + (c*10) + 5] = prog_invs[c]
		chord_bank[chord_slot + (c*10) + 6] = psn[ prog_bass[c] ]
	endfor
	Call @SetupLayout
@End

@LogCurrentSceneToPresetFormat
	//# Logs current scene to a format which can be added to the preset section
	Log {======= END PRESET LOG =======}
	Log {	 Call @SetupLayout}
	Log {	 LabelPads YOUR PRESET NAME -in curly brackets!! } 
	chord_slot = (current_scene * 100)
	for c = 7 to 0
		c_loc = (chord_slot + (c*10))
		n1 = chord_bank[c_loc]
		n2 = chord_bank[c_loc + 1]
		n3 = chord_bank[c_loc + 2]
		n4 = chord_bank[c_loc + 3]
		d = chord_bank[c_loc + dur_slot]
		i = chord_bank[c_loc + inv_slot]
		Log {	   chord_bank[(current_scene * 100) + }, c*10, {] = [}, n1,{,}, n2,{,}, n3,{,}, n4,{,}, d,{,}, i, {]}
	endfor
	Log {	   scene_bank[},{current_scene * sc_size}, {] = [4,6,}, num_sc_presets+1, {,0,0,0,0,0,0,0,0,-1]}
	Log {elseif scn_preset = }, num_sc_presets+1
	Log {======= START PRESET LOG =======}
	Log {+ Make sure to change YOUR PRESET NAME and put it in curly brackets}
	Log {+ Update num_sc_presets variable to }, num_sc_presets+1,{ in @InitKnobVariables}
	Log {+ Copy this PRESET LOG to the end of @ChangeScenePreset before 'endif'}
	Log {++++++ INSTRUCTIONS FOR ADDING A PRESET +++++}
@End
